//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================

//===== rAthenaCN Script =======================================
//= 郵件發送演示腳本
//===== By: ==================================================
//= Sola、小克
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthenaCN 開發團隊
//===== Description: =========================================
//= 演示如何通過 rAthenaCN 拓展的 sendmail 系列指令,
//= 來給玩家發送各種不同類型的郵件.
//============================================================
//= 此腳本演示的指令請見 doc/rAthenaCN_script_commands.txt
//= 相關指令名稱為: sendmail,sendmail2,sendmail3
//============================================================

// -----------------------------------------------------------
// 給定一個角色編號, 返回這個角色編號是否有效
// 用法: callfunc("verify_charid", <角色編號>);
// 返回值: 返回 true 為有效, 返回 false 為無效
// -----------------------------------------------------------
function	script	verify_charid	{
	.@char_id = getarg(0);
	
	// 如果在不確定角色編號是否有效的情況下, 可以使用 query_sql 方法進行檢查:
	.@record_count = query_sql(sprintf("select `char_id` from `char` where `char_id` = %d", .@char_id), .cid);
	
	// 這裡的 .@record_count 是數據庫返回的記錄數, 查詢到的記錄如果不等於 1 條, 那麼返回 FALSE
	return (.@record_count != 1 ? false : true);
}

// -----------------------------------------------------------
// 找出合適指定裝備的卡片, 並填充到角色臨時數組 @card_filled_ret
// 用法: callfunc("card_filled", <裝備的物品編號>)
// -----------------------------------------------------------
function	script	card_filled	{
	.@equip_id = getarg(0);
	deletearray @card_filled_ret;
	
	// 獲取這件裝備的可用卡槽數
	.@slot_ret = getiteminfo(.@equip_id, 10);
	
	// 如果返回值為 -1 那麼說明這件裝備不存在
	// 如果返回值為 0  那麼說明這件裝備沒有卡槽
	if (.@slot_ret <= 0) {
		return;
	}
	
	// 記錄這件裝備的穿戴位置, 以便匹配合適的卡片
	.@equip_loc = getiteminfo(.@equip_id, 5);
	
	// 接下來再啟動一個循環, 嘗試為這件裝備找出合適的卡片
	.@j = .@slot_ret;
	while (.@j > 0) {
		.@card_id = rand(4001,4479);
		
		// 獲取這張卡片的插入位置
		.@loc_ret = getiteminfo(.@card_id, 5);
		
		// 如果返回值為 -1 則表示這個卡片是不存在的, 重新找一件
		if (.@loc_ret < 0) continue;
		
		// 如果卡片的插入位置和裝備的穿戴位置不一致
		// 那麼說明這張卡片不合適這件裝備, 重新找一件
		// 這裡應該使用與運算
		if ((.@equip_loc & .@loc_ret) == 0) continue;
		
		@card_filled_ret[.@slot_ret - .@j] = .@card_id;
		
		.@j--;
	}
	
	return;
}

prontera,169,125,3	script	郵件發送演示	123,{
	mes "[rAthenaCN 演示]";
	mes "我負責演示如何使用腳本指令直接發送郵件給指定角色編號的玩家.";
	mes "與郵件發送配套的腳本指令如下:";
	mes "^ff0000sendmail  - 普通附件^000000";
	mes "^ff0000sendmail2 - 附件可攜帶卡片^000000";
	mes "^ff0000sendmail3 - 附件可攜帶隨機屬性^000000";
	next;
	mes "[rAthenaCN 演示]";
	mes "如果客戶端版本號小於 ^ff000020150513^000000, 那麼演示腳本發送的附件將為^0000ff一個^000000物品.";
	mes "如果客戶端支持 RODEX, 那麼演示腳本發送的附件將為^0000ff多個^000000物品.";
	next;
	mes "[rAthenaCN 演示]";
	mes "此演示NPC的腳本位於服務端的: ^ff0000npc\\racn\\racn_sendmail.txt^000000";
	mes "您可以在下面的配置文件中移除它: ^0000ffnpc\\scripts_racn.conf^000000";
	next;
	
	.@dest_charid = getcharid(0);	// 收件人的角色編號 - 這裡設置為自己
	.@body$ = "這是一封演示郵件\n發送時間為: " + gettimestr("%Y-%m-%d %H:%M:%S",21) + "\n\n支持使用/\n作為換行符.";
	
	// 需要注意的是: sendmail 系列指令並不會去檢查角色編號是否存在, 所以需要自己檢查 
	if (!callfunc("verify_charid", .@dest_charid)) {
		mes "[rAthenaCN 演示]";
		mes "抱歉, 收件人的角色編號無效.";
		close;
	}
	
	switch(select("發送郵件 - 無附件:發送郵件 - 普通附件:發送郵件 - 附件攜帶卡片:發送郵件 - 附件攜帶卡片和隨機屬性")) {
		case 1:
			// 設置郵件標題
			.@title$ = "無附件";
			
			// sendmail(<收件人角色編號>,"<發送者名稱>","<標題>","<內容>"{,<附件中的金幣>
			// {,<附件一的物品編號>,<附件一的物品數量>
			// {,<附件二的物品編號>,<附件二的物品數量>
			// {,<附件三的物品編號>,<附件三的物品數量>
			// {,<附件四的物品編號>,<附件四的物品數量>
			// {,<附件五的物品編號>,<附件五的物品數量>}}}}}});
			
			// 發送郵件
			sendmail .@dest_charid,"rAthenaCN 演示",.@title$,.@body$;
			
			break;
		case 2:
			// 設置郵件標題
			.@title$ = "普通道具附件";
			
			// 隨機初始化5個道具
			for (.@i = 0; .@i < 5; .@i++) {
				.@nameid[.@i] = rand(501,523);
			}
			
			// 隨機初始化附件金額
			.@zeny = rand(1,10000);
			
			// sendmail(<收件人角色編號>,"<發送者名稱>","<標題>","<內容>"{,<附件中的金幣>
			// {,<附件一的物品編號>,<附件一的物品數量>
			// {,<附件二的物品編號>,<附件二的物品數量>
			// {,<附件三的物品編號>,<附件三的物品數量>
			// {,<附件四的物品編號>,<附件四的物品數量>
			// {,<附件五的物品編號>,<附件五的物品數量>}}}}}});
			
			// 發送郵件, 道具的數量為隨機 1~3 個
			sendmail .@dest_charid,"rAthenaCN 演示",.@title$,.@body$,.@zeny,
				.@nameid[0],rand(1,3),
				.@nameid[1],rand(1,3),
				.@nameid[2],rand(1,3),
				.@nameid[3],rand(1,3),
				.@nameid[4],rand(1,3);
			
			break;
		case 3:
			// 設置郵件標題
			.@title$ = "攜帶卡片";
			
			// 為了演示附件能攜帶卡片, 我們需要找出能插卡的武器或者防具
			// 並記住他們能插多少卡, 然後為他們隨機弄一些武器卡片來插入
			// 最後再隨機給他們生成一個 1~12 的精煉值
			
			// 隨機初始化附件金額
			.@zeny = rand(1,10000);
			
			.@i = 5;
			while (.@i > 0) {
				.@equip_id = rand(1101,2373);	// 隨機獲得一個裝備的編號(可能是武器, 也可能是防具)
				
				// 獲取這件裝備的可用卡槽數
				.@slot_ret = getiteminfo(.@equip_id, 10);
				
				// 如果返回值為 -1 那麼說明這件裝備不存在, 重新找一件
				// 如果返回值為 0  那麼說明這件裝備沒有卡槽, 也需要重新找一件
				if (.@slot_ret <= 0) continue;
				
				// 記住這件裝備的編號到數組 .@nameid[] 中
				// 這裡的 .@i 最大值是 5 (表示含 6 個元素), 但是數組下標是 0, 所以應該減去 1,
				// 這樣有效的數組元素數量才是真正的 5 個: 0,1,2,3,4
				.@nameid[5-.@i] = .@equip_id;
				
				// 嘗試獲取這個裝備的類型是武器還是防具, 以便匹配合適的卡片
				if (getiteminfo(.@equip_id, 2) == 4) {
					// 物品類型如果為 4 應該是防具, 參考: doc/item_db.txt
					.@type = 0;		// 我們用 0 表示防具
				}
				else if (getiteminfo(.@equip_id, 2) == 5) {
					// 物品類型如果為 5 應該是武器, 參考: doc/item_db.txt
					.@type = 1;		// 我們用 1 表示武器
				}
				else {
					// 不是防具也不是武器, 按道理不可能出現
					// 除非上面我給定的 1101~2373 物品編號區間出現了影子裝備
					// 這種情況不考慮, 重新選擇道具
					continue;
				}
				
				// 記錄這件裝備的穿戴位置, 以便匹配合適的卡片
				.@equip_loc = getiteminfo(.@equip_id, 5);
				
				// 找出合適這件裝備的卡片, 並將其賦值到 
				callfunc("card_filled", .@equip_id);
				
				// 我們會有多個裝備, 為了區分多個裝備的卡片數組
				// 使用 setd 來實現這個目的, 讓數組名稱能帶上與 .@nameid[] 數組匹配的下標
				// 
				// 比如:
				// .@equip_0_cards[]
				// .@equip_1_cards[]
				// ...
				for (.@k = 0; .@k < getarraysize(@card_filled_ret); .@k++){
					.@var$ = sprintf(".@equip_%d_cards[%d]", (5-.@i), .@k);
					setd .@var$, @card_filled_ret[.@k];
				}
				
				.@i--;
			}
			
			// 提示: 這裡的說明有點長, 建議將文本編輯器調整為：不自動換行, 這樣看起來會好一些
			// 
			// sendmail2(<收件人角色編號>,"<發送者名稱>","<標題>","<內容>"{,<附件中的金幣>
			// {,<附件一的物品編號>,<附件一的物品數量>,<附件一是否已鑒定>,<附件一的精煉值>,<附件一的屬性>,<附件一的第一張卡片>,<附件一的第二張卡片>,<附件一的第三張卡片>,<附件一的第四張卡片>
			// {,<附件二的物品編號>,<附件二的物品數量>,<附件二是否已鑒定>,<附件二的精煉值>,<附件二的屬性>,<附件二的第一張卡片>,<附件二的第二張卡片>,<附件二的第三張卡片>,<附件二的第四張卡片>
			// {,<附件三的物品編號>,<附件三的物品數量>,<附件三是否已鑒定>,<附件三的精煉值>,<附件三的屬性>,<附件三的第一張卡片>,<附件三的第二張卡片>,<附件三的第三張卡片>,<附件三的第四張卡片>
			// {,<附件四的物品編號>,<附件四的物品數量>,<附件四是否已鑒定>,<附件四的精煉值>,<附件四的屬性>,<附件四的第一張卡片>,<附件四的第二張卡片>,<附件四的第三張卡片>,<附件四的第四張卡片>
			// {,<附件五的物品編號>,<附件五的物品數量>,<附件五是否已鑒定>,<附件五的精煉值>,<附件五的屬性>,<附件五的第一張卡片>,<附件五的第二張卡片>,<附件五的第三張卡片>,<附件五的第四張卡片>}}}}}});
			
			sendmail2 .@dest_charid,"rAthenaCN 演示",.@title$,.@body$,.@zeny,
				.@nameid[0],1,1,rand(1,12),0,.@equip_0_cards[0],.@equip_0_cards[1],.@equip_0_cards[2],.@equip_0_cards[3],
				.@nameid[1],1,1,rand(1,12),0,.@equip_1_cards[0],.@equip_1_cards[1],.@equip_1_cards[2],.@equip_1_cards[3],
				.@nameid[2],1,1,rand(1,12),0,.@equip_2_cards[0],.@equip_2_cards[1],.@equip_2_cards[2],.@equip_2_cards[3],
				.@nameid[3],1,1,rand(1,12),0,.@equip_3_cards[0],.@equip_3_cards[1],.@equip_3_cards[2],.@equip_3_cards[3],
				.@nameid[4],1,1,rand(1,12),0,.@equip_4_cards[0],.@equip_4_cards[1],.@equip_4_cards[2],.@equip_4_cards[3];
			
			break;
		case 4:
			
			if (PACKETVER < 20150225) {
				mes "[rAthenaCN 演示]";
				mes "物品隨機屬性機制僅在版本號 ^0000ff大於^000000 ^ff000020150225^000000 的客戶端上有效.";
				mes "請使用符合要求的新版客戶端程序之後, 再來找我演示吧.";
				close;
			}
		
			// 設置郵件標題
			.@title$ = "帶卡片和隨機屬性";
			
			// 除了上面單純的設置卡片, 又多了一個隨機屬性
			// 為了方便隨機賦值, 我們挑出一些用來增加百分比的隨機屬性
			// 然後隨機給裝備賦予這些屬性
			
			// 隨機初始化附件金額
			.@zeny = rand(1,10000);
			
			.@i = 5;
			while (.@i > 0) {
				.@equip_id = rand(1101,2373);	// 隨機獲得一個裝備的編號(可能是武器, 也可能是防具)
				
				// 獲取這件裝備的可用卡槽數
				.@slot_ret = getiteminfo(.@equip_id, 10);
				
				// 如果返回值為 -1 那麼說明這件裝備不存在, 重新找一件
				// 如果返回值為 0  那麼說明這件裝備沒有卡槽, 也需要重新找一件
				if (.@slot_ret <= 0) continue;
				
				// 記住這件裝備的編號到數組 .@nameid[] 中
				// 這裡的 .@i 最大值是 5 (表示含 6 個元素), 但是數組下標是 0, 所以應該減去 1,
				// 這樣有效的數組元素數量才是真正的 5 個: 0,1,2,3,4
				.@nameid[5-.@i] = .@equip_id;
				
				// 嘗試獲取這個裝備的類型是武器還是防具, 以便匹配合適的卡片
				if (getiteminfo(.@equip_id, 2) == 4) {
					// 物品類型如果為 4 應該是防具, 參考: doc/item_db.txt
					.@type = 0;		// 我們用 0 表示防具
				}
				else if (getiteminfo(.@equip_id, 2) == 5) {
					// 物品類型如果為 5 應該是武器, 參考: doc/item_db.txt
					.@type = 1;		// 我們用 1 表示武器
				}
				else {
					// 不是防具也不是武器, 按道理不可能出現
					// 除非上面我給定的 1101~2373 物品編號區間出現了影子裝備
					// 這種情況不考慮, 重新選擇道具
					continue;
				}
				
				// 記錄這件裝備的穿戴位置, 以便匹配合適的卡片
				.@equip_loc = getiteminfo(.@equip_id, 5);
				
				// 找出合適這件裝備的卡片, 並將其賦值到 
				callfunc("card_filled", .@equip_id);
				
				// 我們會有多個裝備, 為了區分多個裝備的卡片數組
				// 使用 setd 來實現這個目的, 讓數組名稱能帶上與 .@nameid[] 數組匹配的下標
				// 
				// 比如:
				// .@equip_0_cards[]
				// .@equip_1_cards[]
				// ...
				for (.@k = 0; .@k < getarraysize(@card_filled_ret); .@k++){
					.@var$ = sprintf(".@equip_%d_cards[%d]", (5-.@i), .@k);
					setd .@var$, @card_filled_ret[.@k];
				}
				
				// 找出五條隨機屬性, 然後讓他們的值隨機在 1~100 之間
				for (.@k = 0; .@k < 5; .@k++) {
					.@ranopt_id = rand(1,75);
					
					setd sprintf(".@equip_%d_optid[%d]", (5-.@i), .@k), .@ranopt_id;
					setd sprintf(".@equip_%d_value[%d]", (5-.@i), .@k), rand(1,100);
					setd sprintf(".@equip_%d_param[%d]", (5-.@i), .@k), 0;
				}
				
				.@i--;
			}
			
			// 提示: 這裡的說明有點長, 建議將文本編輯器調整為：不自動換行, 這樣看起來會好一些
			// 
			// sendmail3(<收件人角色編號>,"<發送者名稱>","<標題>","<內容>"{,<附件中的金幣>
			// {,<附件一的物品編號>,<附件一的物品數量>,<附件一是否已鑒定>,<附件一的精煉值>,<附件一的屬性>,<附件一的第一張卡片>,<附件一的第二張卡片>,<附件一的第三張卡片>,<附件一的第四張卡片>,<附件一的RandomIDArray>,<附件一的RandomValueArray>,<附件一的RandomParamArray>
			// {,<附件二的物品編號>,<附件二的物品數量>,<附件二是否已鑒定>,<附件二的精煉值>,<附件二的屬性>,<附件二的第一張卡片>,<附件二的第二張卡片>,<附件二的第三張卡片>,<附件二的第四張卡片>,<附件二的RandomIDArray>,<附件二的RandomValueArray>,<附件二的RandomParamArray>
			// {,<附件三的物品編號>,<附件三的物品數量>,<附件三是否已鑒定>,<附件三的精煉值>,<附件三的屬性>,<附件三的第一張卡片>,<附件三的第二張卡片>,<附件三的第三張卡片>,<附件三的第四張卡片>,<附件三的RandomIDArray>,<附件三的RandomValueArray>,<附件三的RandomParamArray>
			// {,<附件四的物品編號>,<附件四的物品數量>,<附件四是否已鑒定>,<附件四的精煉值>,<附件四的屬性>,<附件四的第一張卡片>,<附件四的第二張卡片>,<附件四的第三張卡片>,<附件四的第四張卡片>,<附件四的RandomIDArray>,<附件四的RandomValueArray>,<附件四的RandomParamArray>
			// {,<附件五的物品編號>,<附件五的物品數量>,<附件五是否已鑒定>,<附件五的精煉值>,<附件五的屬性>,<附件五的第一張卡片>,<附件五的第二張卡片>,<附件五的第三張卡片>,<附件五的第四張卡片>,<附件五的RandomIDArray>,<附件五的RandomValueArray>,<附件五的RandomParamArray>}}}}}});
			
			sendmail3 .@dest_charid,"rAthenaCN 演示",.@title$,.@body$,.@zeny,
				.@nameid[0],1,1,rand(1,12),0,.@equip_0_cards[0],.@equip_0_cards[1],.@equip_0_cards[2],.@equip_0_cards[3],.@equip_0_optid,.@equip_0_value,.@equip_0_param,
				.@nameid[1],1,1,rand(1,12),0,.@equip_1_cards[0],.@equip_1_cards[1],.@equip_1_cards[2],.@equip_1_cards[3],.@equip_1_optid,.@equip_1_value,.@equip_1_param,
				.@nameid[2],1,1,rand(1,12),0,.@equip_2_cards[0],.@equip_2_cards[1],.@equip_2_cards[2],.@equip_2_cards[3],.@equip_2_optid,.@equip_2_value,.@equip_2_param,
				.@nameid[3],1,1,rand(1,12),0,.@equip_3_cards[0],.@equip_3_cards[1],.@equip_3_cards[2],.@equip_3_cards[3],.@equip_3_optid,.@equip_3_value,.@equip_3_param,
				.@nameid[4],1,1,rand(1,12),0,.@equip_4_cards[0],.@equip_4_cards[1],.@equip_4_cards[2],.@equip_4_cards[3],.@equip_4_optid,.@equip_4_value,.@equip_4_param;
			
			break;
	}
	
	mes "[rAthenaCN 演示]";
	mes "郵件已經發送, 請查收您的郵箱.";
	close;
OnInit:
	waitingroom "郵件發送演示",1;
	end;
}