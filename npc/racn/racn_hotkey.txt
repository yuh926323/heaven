//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================

//===== rAthenaCN Script =======================================
//= 快捷鍵設置演示腳本
//===== By: ==================================================
//= Sola、小克
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthenaCN 開發團隊
//===== Description: =========================================
//= 演示如何通過 gethotkey 和 sethotkey 指令來獲取和設置快捷鍵
//============================================================
//= 此腳本演示的指令為:
//= [腳本] gethotkey <快捷鍵位置編號>{,<要獲取的數據類型>};
//=        為了兼容 SeAr 腳本 get_hotkey 等效於 gethotkey
//=
//= [腳本] sethotkey <快捷鍵位置編號>,<快捷鍵的類型>,<物品/技能的ID>,<技能等級>;
//=        為了兼容 SeAr 腳本 set_hotkey 等效於 sethotkey
//============================================================

prontera,130,125,3	script	快捷鍵設置	123,{
	mes "[rAthenaCN 演示]";
	mes "此演示NPC的腳本位於服務端的: ^ff0000npc\\racn\\racn_hotkey.txt^000000";
	mes "您可以在下面的配置文件中移除它: ^0000ffnpc\\scripts_racn.conf^000000";
	next;
	switch(select("列出全部快捷鍵詳情:給兩個獨立快捷鍵設置上道具")){
		case 1:
			callfunc("F_HOTKEY_LIST_ALL_INFO");
		break;
		case 2:
			getitem 601,1;			// 獲取一個蒼蠅翅膀
			getitem 602,1;			// 獲取一個蝴蝶翅膀
			sethotkey 36,0,601,0;	// 將蒼蠅翅膀設置到36號快捷鍵位置上
			sethotkey 37,0,602,0;	// 將蝴蝶翅膀設置到37號快捷鍵位置上
		break;
	}
	end;
OnInit:
	waitingroom "快捷鍵設置演示",1;
	end;
}

function	script	F_HOTKEY_LIST_ALL_INFO	{
	for (.@i = 0; .@i <= 37; .@i++){
		.@ret = gethotkey(.@i);	// .@ret 中保存著調用函數之後的返回值
		
		// 如果返回值為 1 表示成功獲取到了快捷鍵信息
		// 成功獲取到信息後, 以下幾個變量將保存被查詢的快捷鍵信息
		// -------------------------------------------------------
		// @hotkey_type		快捷鍵的類型 - 0為物品, 1為技能
		// @hotkey_id		物品/技能的編號
		// @hotkey_lv		當快捷鍵類型為1時, 這裡保存著快捷鍵中此技能的等級
		// -------------------------------------------------------
		if (.@ret == 1){
			.@mes$ = "[快捷位置編號: " + sprintf("%02d", .@i) + "] ";
			if (@hotkey_type == 0){
				.@mes$ = .@mes$ + "快捷鍵類型為 0 是一個物品 | ";
				.@mes$ = .@mes$ + "物品編號為 " + sprintf("%4d", @hotkey_id);
			}
			else {
				.@mes$ = .@mes$ + "快捷鍵類型為 1 是一個技能 | ";
				.@mes$ = .@mes$ + "技能編號為 " + sprintf("%4d", @hotkey_id) + " | ";
				.@mes$ = .@mes$ + "技能等級為 " + @hotkey_lv;
			}
			
			dispbottom .@mes$;
		}
		
		// 如果返回值為 -1 則表示獲取失敗
		if (.@ret == -1){
			dispbottom "[快捷位置編號: " + sprintf("%02d", .@i) + "] 獲取快捷鍵信息失敗.";
		}
	}
	return;
}