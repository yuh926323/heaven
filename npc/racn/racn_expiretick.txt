//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================

//===== rAthenaCN Script =======================================
//= 獲取裝備租賃時間演示腳本
//===== By: ==================================================
//= Sola、小克
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthenaCN 開發團隊
//===== Description: =========================================
//= 演示如何通過 getequipexpiretick 和 setrenttime 指令來獲取、
//= 調整指定裝備位置的剩餘租賃時間
//============================================================
//= 此腳本演示的指令為:
//= [腳本] getequipexpiretick <裝備位置編號>{,<角色編號>};
//=        為了兼容 SeAr 腳本 isrental 等效於 getequipexpiretick
//=
//= [腳本] setrenttime <裝備位置編號>,<要增加/減少的租賃時間秒數>{,<角色編號>};
//=        為了兼容 SeAr 腳本 resume 等效於 setrenttime
//============================================================

prontera,124,125,4	script	裝備租賃時間	123,{
	mes "[rAthenaCN 演示]";
	mes "我負責演示獲取裝備剩餘租賃時間.";
	mes "請選擇您要的操作..";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW, EQI_COSTUME_HEAD_LOW, EQI_COSTUME_HEAD_MID, EQI_COSTUME_HEAD_TOP, EQI_COSTUME_GARMENT, EQI_AMMO, EQI_SHADOW_ARMOR, EQI_SHADOW_WEAPON, EQI_SHADOW_SHIELD, EQI_SHADOW_SHOES, EQI_SHADOW_ACC_L, EQI_SHADOW_ACC_R;
	switch(select("租賃裝備以便測試:獲取裝備剩餘租賃時間:增加/減少裝備的租賃時間")){
		case 1:
			mes "[rAthenaCN 演示]";
			mes "請填寫要租賃的裝備編號";
			next;
			input .@nameid;
			if (.@nameid <= 0) break;
			mes "[rAthenaCN 演示]";
			mes "請填寫要租賃的時長(秒)";
			next;
			input .@renttime;
			if (.@renttime <= 0) break;
			mes "[rAthenaCN 演示]";
			mes "租賃完畢, 請穿戴裝備, 隨後再嘗試獲取裝備的剩餘租賃時間.";
			rentitem .@nameid, .@renttime;
			break;
		case 2:
			mes "[rAthenaCN 演示]";
			mes "請選擇要獲取剩餘租賃時間的裝備位置.";
			next;
			.@sel = select("頭飾上:盔甲:角色左手(武器, 盾牌):角色右手(武器):披肩:鞋子:飾品(角色右側):飾品(角色左側):頭飾中:頭飾下:時裝頭飾下:時裝頭飾中:時裝頭飾上:時裝披肩:箭矢/彈藥:影子裝備 - 盔甲:影子裝備 - 武器:影子裝備 - 盾牌:影子裝備 - 鞋子:影子裝備 - 飾品(角色左側):影子裝備 - 飾品(角色右側)");
			.@sel = .@indices[.@sel];
			.@ret = getequipexpiretick(.@sel);
			mes "[rAthenaCN 演示]";
			switch(.@ret){
				case -1:
					mes "這個位置沒有穿戴任何裝備";
					break;
				case 0:
					mes "這是普通裝備, 不會過期";
					break;
				default:
					mes "剩餘租賃時間為:" + isrental(.@sel) + " 秒";
					break;
			}
			break;
		case 3:
			mes "[rAthenaCN 演示]";
			mes "請選擇要調整剩餘租賃時間的裝備位置.";
			next;
			.@sel = select("頭飾上:盔甲:角色左手(武器, 盾牌):角色右手(武器):披肩:鞋子:飾品(角色右側):飾品(角色左側):頭飾中:頭飾下:時裝頭飾下:時裝頭飾中:時裝頭飾上:時裝披肩:箭矢/彈藥:影子裝備 - 盔甲:影子裝備 - 武器:影子裝備 - 盾牌:影子裝備 - 鞋子:影子裝備 - 飾品(角色左側):影子裝備 - 飾品(角色右側)");
			.@sel = .@indices[.@sel];
			mes "[rAthenaCN 演示]";
			mes "- 要增加租賃時間, 請填寫正數";
			mes "- 要減少租賃時間, 請填寫負數";
			mes "- 租賃時間單位為: ^ff0000秒^000000";
			mes "- 填寫 0 則取消操作";
			next;
			input .@renttime,-1000000,1000000;
			.@ret = setrenttime(.@sel, .@renttime);
			mes "[rAthenaCN 演示]";
			switch(.@ret){
				case -1:
					mes "這個位置沒有穿戴任何裝備";
					break;
				case 0:
					mes "這是普通裝備, 無法調整租賃時間";
					break;
				default:
					mes "調整後的租賃時間為:" + .@ret + " 秒";
					break;
			}
			break;
	}
	close;
OnInit:
	waitingroom "裝備租賃時間",1;
	end;
}