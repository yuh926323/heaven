//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================


-	script	RACN_IPLIMIT	-1,{
end;
OnPCLoginEvent:
	.@max_limit = 1;	// 每個IP最多登錄的玩家個數
	
	// =========================================================
	// *getsameipinfo({<"想查找的IP地址">})
	// 這裡「想查找的IP地址」是可選參數，若不填寫則默認等於當前關聯到腳本的玩家的IP地址
	// =========================================================
	
	getsameipinfo();
	
	// =========================================================
	// 調用 getsameipinfo(); 後, 系統會返回以下參數
	// @sameip_amount	-	IP地址相同的玩家數量(包括當前玩家在內)
	// @sameip_cid[]	-	IP地址相同的玩家的角色編號數組
	// @sameip_aid[]	-	IP地址相同的賬號的賬號編號數組
	// @sameip_name$[]	-	IP地址相同的角色名稱數組(記住是字符串類型, 要有 $ 符號)
	// =========================================================
	if (@sameip_amount - 1 >= .@max_limit){
		mes "[多開限制檢測]";
		mes "您觸發了多開限制演示腳本,";
		mes "此演示NPC的腳本位於服務端的: ^ff0000npc\\racn\\racn_iplimit.txt^000000";
		mes "您可以在下面的配置文件中移除它: ^0000ffnpc\\scripts_racn.conf^000000";
		next;
		mes "[多開限制檢測]";
		mes "您的IP地址為: ^ff0000" + getcharip() + "^000000.";
		mes "該IP已經有以下角色在線了:";
		
		for (.@i = 0; .@i < @sameip_amount; .@i++){
			if (@sameip_cid[.@i] != getcharid(0)){
				mes "- ^0055ff" + @sameip_name$[.@i] + "^000000";
			}
		}
		mes "本服每IP最多只有 ^ff0000" + .@max_limit + "^000000 個角色在線.";
		mes "伺服器拒絕您本次登錄.";
		
		// 這裡要等一會兒，避免用戶還沒loading完畢就被踢，體驗好一些
		sleep2 2000;
		logout 4;	// 以 4 號理由將玩家踢下線, 可選的理由參數等請等待相關文檔中說明
		close;
	}
end;
}