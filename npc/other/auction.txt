//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================

//===== rAthena Script =======================================
//= Auction system
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Auction house releated NPCs, and warps
//===== Additional Comments: =================================
//= 1.0 First version, place holders only. [L0ne_W0lf]
//= 1.1 Moved Morroc Auction NPCs to Morroc Ruins. [L0ne_W0lf]
//= 1.2 Warp fixed. Dialog in Ruins/Lighthalzen changed. [Kisuka]
//= 1.3 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//============================================================

// Auction House warpers
//============================================================
moc_ruins,78,173,6	script	拍賣行嚮導#moc	98,{
	callfunc "F_AuctionWarper",1;
}

prontera,218,120,4	script	拍賣行嚮導#prt	117,{
	callfunc "F_AuctionWarper",2;
}

yuno,129,116,0	script	拍賣行嚮導#yuno	90,{
	callfunc "F_AuctionWarper",3;
}

lighthalzen,205,169,6	script	拍賣行嚮導#lhz	833,{
	callfunc "F_AuctionWarper",4;
}

function	script	F_AuctionWarper	{
	mes "[拍賣行嚮導]";
	mes "你好, 請問是否想要前往拍賣大廳?";
	next;
	if (select("是的:不要") == 1) {
		.@num = getarg(0);
		if (.@num == 1 || .@num == 4) {
			mes "[拍賣行嚮導]";
			mes "好的! 希望你能找到想買的東西~";
		} else {
			mes "[拍賣行嚮導]";
			mes "請享受這讓人怦然心動的拍賣會.";
		}
		close2;
		switch(.@num) {
		case 1:
			warp "auction_01",179,53; end;
		case 2:
			warp "auction_01",21,43; end;
		case 3:
			warp "auction_02",151,23; end;
		case 4:
			warp "auction_02",43,24; end;
		}
	}
	mes "[拍賣行嚮導]";
	mes "好吧, 下次見.";
	mes "如果您改變了主意, 歡迎再來.";
	close;
}

// Sign posts
//============================================================
-	script	Information Post#dum::AuctionSign	-1,{
	mes "[告示板]";
	mes "和附近的「拍賣行嚮導」對話,";
	mes "可以傳送值拍賣大廳.";
	mes "在那裡你可以拍賣自己的物品,";
	mes "或者競拍其他人正在拍賣中的物品.";
	close;
}

moc_ruins,76,176,6	duplicate(AuctionSign)	告示板#moc	837
prontera,216,120,4	duplicate(AuctionSign)	告示板#prt	837
yuno,131,116,0	duplicate(AuctionSign)	告示板#yuno	837
lighthalzen,207,169,6	duplicate(AuctionSign)	告示板#lhz	837

// Warps
//============================================================
auction_01,180,49,0	warp	auction_entrance_moc	1,1,moc_ruins,78,171
auction_01,22,37,0	warp	auction_entrance_prt	1,1,prontera,214,120
auction_02,151,17,0	warp	auction_enterance_juno	1,1,yuno,132,119
auction_02,43,17,0	warp	auction_enterance_lhz	1,1,lighthalzen,209,169

// Auction House NPCs
//============================================================
-	script	Auction Broker#dum::AuctionBroker	-1,{
	mes "[拍賣師]";
	mes "歡迎來到拍賣大廳.";
	mes "請問是否想瞭解正在拍賣的好東西呢?";
	next;
	if (select("是的:不要") == 1) {
		mes "[拍賣師]";
		mes "好的, 在開價之前, 請仔細瞭解它們.";
		OpenAuction;
		close;
	}
	mes "[拍賣師]";
	mes "好吧, 如果您改變了主意, 歡迎再來.";
	close;
}

auction_01,182,68,6	duplicate(AuctionBroker)	拍賣師#moc1	98
auction_01,182,75,0	duplicate(AuctionBroker)	拍賣師#moc2	99
auction_01,177,75,2	duplicate(AuctionBroker)	拍賣師#moc3	98
auction_01,177,68,4	duplicate(AuctionBroker)	拍賣師#moc4	99
auction_01,21,74,4	duplicate(AuctionBroker)	拍賣師#prt1	117
auction_01,27,78,4	duplicate(AuctionBroker)	拍賣師#prt2	116
auction_01,16,78,4	duplicate(AuctionBroker)	拍賣師#prt3	115
auction_02,158,47,6	duplicate(AuctionBroker)	拍賣師#yuno1	90
auction_02,145,47,2	duplicate(AuctionBroker)	拍賣師#yuno2	90
auction_02,151,54,0	duplicate(AuctionBroker)	拍賣師#yuno3	90
auction_02,152,41,4	duplicate(AuctionBroker)	拍賣師#yuno4	90
auction_02,57,46,2	duplicate(AuctionBroker)	拍賣師#lhz1	874
auction_02,31,46,6	duplicate(AuctionBroker)	拍賣師#lhz2	874
auction_02,43,65,4	duplicate(AuctionBroker)	拍賣師#lhz3	833
