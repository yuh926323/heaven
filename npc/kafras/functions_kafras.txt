//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================

//===== rAthena Script ======================================= 
//= Kafra Functions
//===== By: ================================================== 
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 6.8
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: ================================= 
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should. 
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Updated some functions and added VIP features. [Euphy]
//============================================================ 

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	.@welcome = getarg(0);
	.@menu_num = getarg(1);

	// Display Kafra Welcome Message
	switch(.@welcome){
		default:
		case 0:
			// Default message (obsolete)
			mes "[卡普拉 職員]";
			mes "歡迎光臨!";
			mes "卡普拉公司永遠為您提供最優質的服務!";
			mes "您需要什麼服務呢?";
			break;
		case 1:
			// Niflheim specific message
			mes "[卡普拉 職員]";
			mes "^666666歡迎...來到...";
			mes "卡普拉公司...";
			mes "我們...到死...都會繼續為您服務...^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			set @GID,getcharid(2);
			mes "[卡普拉 職員]";
	 		mes "歡迎光臨. ^ff0000" + GetGuildName(@GID) + "^000000 成員們.";
			mes "卡普拉公司永遠在各位身邊為您服務!";
			break;
		case 3:
			// Amatsu specific message (obsolete)
			mes "[卡普拉 職員]";
			mes "您看起來好像來自一個遙遠的地方, 是來研究我們的文化, 還是來觀光呢?";
			mes "若兩者皆是, 為何不稍稍停留片刻?";
			mes "在這野花香味撲鼻的國度, 相聚是緣.";
			break;		
		case 4:
			// Louyang and Ayothaya specific message (obsolete)
			mes "[卡普拉 職員]";
			mes "與許多的卡普拉服務點一樣,";
			mes "我們會讓你感覺從未離開家鄉.";
			break;		
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	while (1) {
		next;
		deletearray @K_Menu0$[0],getarraysize(@K_Menu0$);
		if (.@welcome == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"使用倉庫","公會倉庫","租用手推車","傳送服務","結束對話";
		} else {
			switch(.@menu_num){
				// Save and Storage only
				case 1:	setarray @K_Menu0$[0],"儲存服務","使用倉庫","公會倉庫","結束對話"; break;
				// Storage only
				case 2:	setarray @K_Menu0$[0],"使用倉庫","公會倉庫","結束對話"; break;
				// No Teleport (Common)
				case 3: setarray @K_Menu0$[0],"儲存服務","使用倉庫","公會倉庫","租用手推車","查詢其他信息","結束對話"; break;
				// Case 4 is Einbroch no tele message.
				// No save, or teleport. (Common)
				case 5:	setarray @K_Menu0$[0],"使用倉庫","公會倉庫","租用手推車","查詢其他信息","結束對話"; break;
				// Storage and Check Other Information only.
				case 6:	setarray @K_Menu0$[0],"使用倉庫","公會倉庫","查詢其他信息","結束對話"; break;
				// Save, Storage, and Pushcart only (Kafra Warehouse)
				case 7:	setarray @K_Menu0$[0],"儲存服務","使用倉庫","公會倉庫","租用手推車","結束對話"; break;
				// Save, Storage, Other Check information. (Turbo track)
				case 8: setarray @K_Menu0$[0],"儲存服務","使用倉庫","公會倉庫","查詢其他信息","結束對話"; break;
				// No Save (Rune Knight)
				case 9: setarray @K_Menu0$[0],"儲存服務","租用手推車","傳送服務","查詢其他信息","結束對話"; break;
				// Storage, Save, and Pushcart (Dewata, reorder of case 7)
				case 10: setarray @K_Menu0$[0],"使用倉庫","公會倉庫","儲存服務","租用手推車","結束對話"; break;
				// Default message (obsolete)
				default: setarray @K_Menu0$[0],"儲存服務","使用倉庫","傳送服務","公會倉庫","租用手推車","查詢其他信息","結束對話"; break;
			}
		}
		set .@menu$,"";
		for (set .@i, 0; .@i < getarraysize(@K_Menu0$); set .@i, .@i + 1)
			set .@menu$, .@menu$ + @K_Menu0$[.@i] + ":";	
		set .@j, select (.@menu$) - 1;
		if (@K_Menu0$[.@j] == "儲存服務") {
			return;
		} else if (@K_Menu0$[.@j] == "使用倉庫") {
			// Do not charge for Guild Storage
			if(.@welcome == 2) 
				callfunc "F_KafStor",2,0,0;
			else 
				callfunc "F_KafStor",0,getarg(3),.@welcome;
		} else if (@K_Menu0$[.@j] == "傳送服務") {
			// Display Einbroch "No Teleport Service" notice.
			if(.@menu_num != 4) callfunc "F_KafTele",.@welcome;
			else {
				mes "[卡普拉 職員]";
				mes "由於卡普拉公司與秀髮茲發德共和國";
				mes "簽訂了 ^FF0000《限制傳送服務協議》^000000";
				mes "所以目前無法為您提供傳送服務!";
				next;
				mes "[卡普拉 職員]";
				mes "如果您想移動到其他區域, 我們推薦您使用飛空艇服務!";
				mes "感謝您的理解與支持.";
			}
		} else if (@K_Menu0$[.@j] == "租用手推車") {
			callfunc "F_KafCart",.@welcome,getarg(4);
		} else if (@K_Menu0$[.@j] == "查詢其他信息") {
			callfunc "F_KafInfo",getarg(2);
		} else if (@K_Menu0$[.@j] == "結束對話"){
			callfunc "F_KafEnd",.@welcome,0;
			end;
		} else if (@K_Menu0$[.@j] == "公會倉庫") {
			callfunc "F_KafStor",1,0;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	.@type = getarg(0);
	.@fee = getarg(1);

	// Unable to access Guild Storage (Busy)
	if(.@type == 1){
		switch(guildopenstorage()){
			case 0:
				mes "[卡普拉 職員]";
				mes "已經成功為您打開公會倉庫!";
				break;
			case 1:
				mes "[卡普拉 職員]";
				mes "真的十分抱歉,";
				mes "目前有其他公會成員正在使用倉庫!";
				mes "請過一段時間再嘗試打開!";
				break;
			case 2:
				mes "[卡普拉 職員]";
				mes "很抱歉, 您至少需要加入一個公會,";
				mes "才可以使用公會倉庫.";
				break;
		}
		cutin "", 255;
		close2;
		end;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(!callfunc("F_CanOpenStorage")){
		mes "[卡普拉 職員]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666對...不...起";
			mes "...你的...基本...技...能...等..級級..";
			mes "要...達到...6級...以上...才...可以...^000000";
			return;
		}
		mes "非常抱歉";
		mes "您的基本技能需要達到6級以上,";
		mes "才能使用倉庫服務!";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(.@type != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<.@fee){
				mes "[卡普拉 職員]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666金金金幣...";
					mes "需要...更..多的...金金...幣...!";
					mes "至少...需要...150...金幣...";
					mes "咕咚! 要不您先付一點血吧~!^000000";
					return;
				}
				// Standard Message
				mes "很抱歉, 您沒有足夠的金幣,";
				mes "訪問倉庫至少需要 "+.@fee+" Zeny.";
				return;
			}
			set Zeny, Zeny-.@fee;
			set RESRVPTS, RESRVPTS + (.@fee/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (.@type != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[卡普拉 職員]";
			for (set .@i, 0; .@i < 5; set .@i, .@i + 1)
				mes "^666666感謝..您的...使...用...^000000";
		}
		// Normal message
		else {
			mes "[卡普拉 職員]";
			mes "現在為您打開倉庫!";
			mes "感謝您使用卡普拉服務.";
		}
	}
	callfunc("F_CheckKafCode", "[卡普拉 職員]");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	mes "[卡普拉 職員]";
	mes "請告訴我您的目的地";
	next;
	set .@menu$, "";
	for (set .@i, 0; .@i <= 6; set .@i, .@i + 1)
		set .@menu$, .@menu$ + @wrpC$[.@i] + ":";
	set .@j, select (.@menu$) - 1;
	if (@wrpC$[.@j] == "結束對話") 
	{
		mes "[卡普拉 職員]";
		mes "如果想用傳送服務, 歡迎您隨時來找我!";
		close2;
		cutin "", 255;
		end;
	}
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
	else {
		if (Zeny<@wrpP[.@j]){
			mes "[卡普拉 職員]";
			mes "您沒有足夠的金幣來使用傳送服務,";
			mes "傳送到 "+@wrpD$[.@j]+" 需要 "+@wrpP[.@j]+" Zeny.";
			close2;
			cutin "", 255;
			end;
		}
		set Zeny, Zeny - @wrpP[.@j];
		set RESRVPTS, RESRVPTS + (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "艾爾帕蘭") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "艾爾貝塔") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "克魔島") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "發樂斯燈塔島") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "吉芬") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "依斯魯得島") { if (checkre(0)) warp "izlude", 128, 98; else warp "izlude", 91, 105; }
	else if (@wrpD$[.@j] == "朱諾") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "妙勒尼廢棄礦場") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "夢羅克") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "獸人村落") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "斐楊") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "普隆德拉") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "汶巴拉") warp "umbala", 100, 154;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(BaseClass != Job_Merchant){
		mes "[卡普拉 職員]";
		mes "很抱歉, 手推車租用服務僅提供給";

		if (checkre(0))
			mes "商人系職業!";
		else {
			mes "商人、鐵匠、煉金術師、創造者";
			mes "等商人系職業!";
		}
		close2;
		cutin "", 255;
		end;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[卡普拉 職員]";
		mes "你已經租用過手推車啦.";
		mes "我們不允許同時租用兩輛手推車!";
		close2;
		cutin "", 255;
		end;
	}
	// Check if the player has the Pushcart skill
	else if(getskilllv("MC_PUSHCART") == 0) {
		mes "[卡普拉 職員]";
		mes "您必須先學習「手推車使用」技能,";
		mes "才可以租用手推車!";
		close2;
		cutin "", 255;
		end;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[卡普拉 職員]";
		mes "租用手推車需要 "+getarg(1)+" Zeny.";
		mes "您想繼續租用手推車嗎?";
		next;
		if (select("租用手推車:取消") == 2) 
		{
			mes "[卡普拉 職員]";
			mes "手推車可以讓隨身攜帶的物品數增加~";
			mes "也可以用來作為武器,";
			mes "如果想擺露天商店的話, 手推車也是必不可少的物品呢.";
			close2;
			cutin "", 255;
			end;
		}
		if(Zeny<getarg(1)){
			mes "[卡普拉 職員]";
			mes "很抱歉, 您並沒有足夠的金錢,";
			mes "租用手推車需要 "+getarg(1)+" Zeny.";
			close2;
			cutin "", 255;
			end;
		}
		set Zeny,Zeny-getarg(1);
		set RESRVPTS, RESRVPTS + 48;
	}
	mes "[卡普拉 職員]";
	mes "手推車租用手續已經登記完畢~";
	mes "要好好愛惜它喲.";
	setcart;
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//	set .@block, 1;
		setarray .@m$, "查看儲蓄點數","倉庫密碼服務","職員位置分佈","結束對話";
		if (getarg(0) == 2) deletearray .@m$[1],2; // Port Malaya, Rune Knight
		else if (.@block) deletearray .@m$[1],1;
	while (1) {
		set .@j, select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "查看儲蓄點數") {
			mes "[卡普拉 職員]";
			mes "讓我看一下...";
			mes "您的名字是..." + strcharinfo(0) + "...";
			mes "儲蓄點數一共是 ^FF0000" + RESRVPTS + " ^000000點!";
			next;
			mes "[卡普拉 職員]";
			mes "在艾爾帕蘭的卡普拉公司總部,";
			mes "您可以把儲蓄點數換成贈品,";
			mes "請多使用無比方便的卡普拉服務, 換取更多的儲蓄點數喲!";
			close2;
			cutin "", 255;
			end;
			if(getarg(0) == 1) return;
		} else if (.@m$[.@j] == "職員位置分佈") {
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			mes "[卡普拉 職員]";
			mes "已經在您的小地圖上標出了本城鎮";
			mes "所有卡普拉職員的位置.";
			next;
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			mes "[卡普拉 職員]";
			mes "看完啦, 記住了嗎?";
			mes "那我就將小地圖上的標記擦掉啦,";
			mes "如果記不住的話, 可以再來找我喲!";
			close2;
			cutin "", 255;
			end;
		} else if (.@m$[.@j] == "倉庫密碼服務") {
			callfunc("F_SetKafCode","[卡普拉 職員]","我們卡普拉公司");
		} else if (.@m$[.@j] == "結束對話") {
			mes "[卡普拉 職員]";
			mes "如果您還有不清楚的地方,";
			mes "歡迎詢問離你最近的卡普拉職員.";
			close2;
			cutin "", 255;
			end;
		} else {
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[卡普拉 職員]";
	if(getarg(1)==1) {	
		// Save
		mes "您的復活點已經存儲到了:";
		mes  "^0000FF"+getarg(2)+"^000000";
		mes "謝謝您使用我們卡普拉公司的服務!";
	} else if(getarg(0)==0 || getarg(0)==5) {	
		// Generic End
		mes "為了讓您舒心的使用卡普拉公司的服務, 我們會盡最大努力提升我們的服務質量.";
		mes "感謝您對我們一如既往的支持.";
	} else if(getarg(0)==1) {
		// Niflheim End
		percentheal 0,- 25;
		mes "^666666我們...卡普拉...公司...";
		mes "會一直...陪伴在...您的...身邊...";
	}
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if(!#kafra_code) return;
	mes "請輸入您的倉庫密碼:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code) {
		next;
		mes getarg(0);
		mes "很抱歉, 倉庫密碼輸入錯誤!";
		mes "無法為您打開倉庫.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	if(#kafra_code) {
		mes "您曾經申請過使用密碼保護倉庫的服務.";
		mes "請問現在想要做什麼事情呢?";
		next;
		switch (select("修改倉庫密碼 -> 5000z:取消倉庫密碼 -> 1000z:結束對話")) {
			case 1:
				mes .@npc_name$;
				mes "首先, 請輸入您的 ^0000FF舊密碼(現有密碼)^000000.";
				set @code,callfunc("F_EntKafCode");
				if(!@code || @code != #kafra_code) {
					mes "呃..密碼錯誤. 您不能修改倉庫密碼!";
					emotion e_hmm;
					break;
				}
				next;
				callsub S_SET,.@npc_name$,.@comp_name$;
				break;
			case 2:
				mes .@npc_name$;
				mes "在取消倉庫密碼之前,";
				mes "請輸入之前設定的密碼來確認您的身份.";
				mes "==============================";
				set @code,callfunc("F_EntKafCode");
				if(!@code) {
					mes "您的倉庫密碼尚未被移除!";
					emotion e_hmm;
					break;
				}
				next;
				mes .@npc_name$;
				if(Zeny < 1000) {
					mes "您沒有足夠的錢用來取消倉庫密碼!";
					emotion e_cash;
					break;
				}
				set Zeny,Zeny-1000;
				if(@code == #kafra_code) {
					set #kafra_code,0;
					mes "您的倉庫密碼已經移除";
					mes "感謝您使用"+.@comp_name$+"的服務.";
					emotion e_thx;
				} else {
					mes "密碼錯誤!";
					mes "您上交的 1000z 我們將不予退回!";
					mes "請想好正確的密碼後, 再來找我吧.";
					emotion e_sry;
				}
				break;
			case 3:
				break;
		}
	} else {
		mes .@comp_name$+"現已經推出全新服務:";
		mes "^0000FF使用密碼來加強倉庫的安全性^000000";
		next;
		switch (select("設置一個新密碼 -> 5000z:結束對話")) {
			case 1: callsub S_SET,.@npc_name$,.@comp_name$; break;
			case 2: 
				mes .@npc_name$;
				mes "您真的不想用嗎?";
				mes "設定好倉庫密碼後就算遊戲賬號被盜,";
				mes "壞蛋也拿不走倉庫的東西喲!";
				close2;
				cutin "", 255;
				end;
			break;
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes .@npc_name$;
	mes "現在, 我們來設定^FF0000新密碼^000000以便保護倉庫!";
	set @code,callfunc("F_EntKafNewCode");
	if(!@code) {
		mes "密碼已經設定完畢!";
		emotion e_hmm;
		return;
	}
	next;
	mes .@npc_name$;
	if(Zeny < 5000) {
		mes "您沒有足夠的金錢!";
		emotion e_cash;
		return;
	}
	set Zeny,Zeny-5000;
	set #kafra_code,@code;
	mes "您的倉庫密碼已啟用!";
	mes "感謝您使用"+.@comp_name$+"的服務.";
	emotion e_thx;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "請輸入倉庫密碼:";
	set @code_,0;
	input @code_;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Tried to fit storage password.";
	}
	return @code_;
}

function	script	F_EntKafNewCode	{
	mes "請輸入一個4-8位數字之間的數字";
	mes "來作為您的倉庫密碼:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>3) {
		set @kafcode_try,0;
		logmes "Hack: Tried to fit storage password.";
	}
	if(input(@code_) == 1) {
		next;
		mes "[卡普拉 職員]";
		mes "汗, 您的密碼好短啊, 不安全呀!";
		mes "請至少輸入一個4位數的密碼.";
		close2;
		cutin "", 255;
		end;
	}
	if(@code_<1000) {
		next;
		mes "[卡普拉 職員]";
		mes "您輸入的密碼太短了, 不安全呀!";
		mes "請至少輸入一個4位數的密碼.";
		close2;
		cutin "", 255;
		end;
	}
	return @code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	cleararray @wrpD$[0],"",getarraysize(@wrpD$);
	cleararray @wrpP$[0],"",getarraysize(@wrpP$);
	cleararray @viewpX[0],"",getarraysize(@viewpX);
	cleararray @viewpY[0],"",getarraysize(@viewpY);
	if (strnpcinfo(4) == "prontera") {
		setarray @wrpD$[0], "依斯魯得島", "吉芬", "斐楊", "夢羅克", "獸人村落", "艾爾貝塔";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (strnpcinfo(4) == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "斐楊", "夢羅克", "普隆德拉";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (strnpcinfo(4) == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "吉芬", "朱諾" , "依斯魯得島", "妙勒尼廢棄礦場";
	} else if (strnpcinfo(4) == "comodo" ) {
		setarray @wrpD$[0], "夢羅克", "發樂斯燈塔島", "汶巴拉";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (strnpcinfo(4) == "cmd_fild07") {
		setarray @wrpD$[0], "克魔島", "夢羅克";
		setarray @wrpP[0], 1200, 1200;
	} else if (strnpcinfo(4) == "geffen") {
		setarray @wrpD$[0], "普隆德拉", "艾爾帕蘭", "獸人村落", "妙勒尼廢棄礦場";
		setarray @wrpP[0], 1200, 1200, 1700, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (strnpcinfo(4) == "izlude") {
		setarray @wrpD$[0], "吉芬", "斐楊", "夢羅克", "艾爾帕蘭";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(4) == "morocc") {
		setarray @wrpD$[0], "普隆德拉", "斐楊", "艾爾貝塔", "克魔島", "發樂斯燈塔島";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (strnpcinfo(4) == "umbala") {
		setarray @wrpD$[0], "克魔島";
		setarray @wrpP[0], 1800;
	} else if (strnpcinfo(4) == "payon") {
		setarray @wrpD$[0], "普隆德拉", "艾爾貝塔", "夢羅克";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (strnpcinfo(4) == "yuno") {
		setarray @wrpD$[0], "艾爾帕蘭";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (strnpcinfo(4) == "job3_rune01") {
		setarray @wrpD$[0], "依斯魯得島", "吉芬", "斐楊", "夢羅克", "艾爾帕蘭";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
			setd "@wrpP["+.@i+"]",getd("@wrpP["+.@i+"]") * 2;
	}

	for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
		setd "@wrpC$["+.@i+"]",getd("@wrpD$["+.@i+"]")+" -> "+getd("@wrpP["+.@i+"]")+" z";
	setd "@wrpC$["+getarraysize(@wrpC$)+"]","結束對話";
	return;
}
