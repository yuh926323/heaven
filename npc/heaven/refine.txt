//====================================================//
//====================================================//
//==製作人 : 陳小修 (SkyTwo) 　　　　　　Eathena　　==//
//==更新日期 : 西元 2008/06/20　　　　　　　Script　==//
//==腳本版號 : 繁體中文 v1.5 正式版　　　　　　　　 ==//
//==腳本名稱 : 阿呆工匠　　　　　　　　　　　　　　 ==//
//==　　　　　　　　　　　　　　　　　　　　　　　　==//
//==腳本出處 : http://srogm.no-ip.info/forum　　　　==//
//==　　　　　　　　　　　　　　　　　　　　　　　　==//
//==各位大哥大姐轉載請保留以上，萬分感激。　　　　　==//
//====================================================//
//====================================================//
que_temsky.gat, 94, 95, 6	script	阿呆工匠	63,{

set .@menu, select(
    "^FF0000精煉裝備^000000",
    "^0000FF濃縮神鋁精練裝備^000000",
    "修理裝備",
    "拆除卡片",
    "小神鋁換大神鋁",
    "選購材料"
);

switch(.@menu) {
    case 1:
        callfunc("RefineMain", 1);
        break;
    case 2:
        callfunc("RefineMain", 2);
         break;
    case 3:
        callfunc "Repair";
        break;
    case 4:
        callfunc "CardMain";
        break;
    case 5:
        callfunc "Change";
        break;
    case 6:
        callfunc "ShopMain";
        break;
}

end;

OnInit:
    set .npcname$, "[阿呆工匠]";
    setarray $@TRANS_SELECT_TEXT$,
        "頭上", "頭中", "頭下", "衣服", "左手武器",
        "右手盾牌", "披肩", "鞋子", "飾品1", "飾品2";
    setarray $@TRANS_SELECT_VALUE,
        EQI_HEAD_TOP, EQI_HEAD_MID, EQI_HEAD_LOW, EQI_ARMOR, EQI_HAND_L,
        EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R;
    end;
}

//====================================================//

function	script	RefineMain	{
	mes .npcname$;
	mes "請選擇您要精煉的物品選項。";
	next;
    set .@RItem, $@TRANS_SELECT_VALUE[select(callfunc("GetRefineSelectMenu")) - 1];

	if ( getequipisequiped(.@RItem) == 0 ) {
        mes .npcname$;
        mes "您還沒有裝備要精煉物品唷!";
        close;
    }
	if ( getequipisenableref(.@RItem) == 0 ) {
        mes .npcname$;
        mes "這個物品是無法精煉的唷!";
        close;
    }
	if ( getequiprefinerycnt(.@RItem) >= 20 ) {
        mes .npcname$;
        mes "這個物品已經精煉到最高等級了唷!";
        close;
    }

    // 鋁、一級強化、二級強化、神之金屬、神之金屬
    setarray .@normal, 985, 1010, 1011, 984, 984;
    // 濃縮鋁、濃縮神之金屬
    setarray .@advance, 7619, 7620, 7620, 7620, 7620;
    setarray .@cost, 20000, 500, 2000, 25000, 50000;
    setarray .@extra_rates, 0, 5;
    set .@mode, getarg(0); // .@mode = 1: 一般; .@mode = 2: 濃縮
    set .@extra_rate, .@extra_rates[.@mode - 1];
    set .@weapon_level, getequipweaponlv(.@RItem);
	switch(.@mode) {
        case 1:
            set .@Item, .@normal[.@weapon_level];
            set .@Zeny, .@cost[.@weapon_level];
            break;
        case 2:
            set .@Item, .@advance[.@weapon_level];
            set .@Zeny, .@cost[.@weapon_level];
            break;
    }

	mes .npcname$ + "[^0000FF計算機^000000][^FF0000" + (getequippercentrefinery(.@RItem) + .@extra_rate) + "%^000000]";
	mes "[^0000FF防爆水晶^000000數量為^FF0000" + countitem(20001) + "^000000個]";
	mes "精煉 ^0000FF" + getequipname(.@RItem) + "^000000 這項物品";
	mes "需要 ^FF0000" + getitemname(.@Item) + " 1^000000 個";
	mes "　和 ^FF0000金錢 " + .@Zeny + "^000000 z";
	next;

	if (select("是的!", "不了!") == 2) {
        close;
    }

    if ( (getequippercentrefinery(.@RItem) + .@extra_rate) < 100 ) {
        mes .npcname$;
        mes "物品已經達到它的^FF0000安定值^000000，";
        mes "繼續精煉可能會使裝備損壞，";
        mes "即使這樣您還要繼續精煉嗎？";
        next;
        if (select("是的!", "不了!") == 2) {
            close;
        }
    }
    mes "[阿呆工匠][^0000FF計算機^000000][^FF0000" + (getequippercentrefinery(.@RItem) + .@extra_rate) + "%^000000]";
    mes "鏘！鏘！鏘！";
    next;

Refine_Ing:
    if ( countitem(.@Item) < 1 || Zeny < .@Zeny ) {
        mes .npcname$;
        mes "抱歉您身上的物品不足!";
        close;
    }

	delitem .@Item,1;
	set Zeny,Zeny - .@Zeny;

    if ( (getequippercentrefinery(.@RItem) + .@extra_rate) >= rand(100) ) {
        successrefitem(.@RItem);
        if ( getequiprefinerycnt(.@RItem) < 20 ) {
            set .@RStr$, "請問";
            mes .npcname$ + "[^0000FF計算機^000000][^FF0000" + (getequippercentrefinery(.@RItem) + .@extra_rate) + "%^000000]";
            mes "[^0000FF防爆水晶^000000數量為^FF0000" + countitem(20001) + "^000000個]";
            if ( (getequippercentrefinery(.@RItem) + .@extra_rate) != 100 ) {
                mes "物品已經達到它的^FF0000安定值^000000，";
                mes "繼續精煉可能會使裝備損壞，";
                set .@RStr$, "即使這樣";
            }
            mes .@RStr$ + "您還要繼續精煉嗎？";
            next;
            if (select("是的!", "不了!") == 1) {
                goto Refine_Ing;
            }
        }
        mes .npcname$;
        mes "恭喜您物品已精煉成功了!";
        close;
    } else {
        if ( countitem(20001) != 0 ) {
            delitem 20001,1;
            specialeffect2 155;
            mes .npcname$;
            mes "原來是防爆水晶阿!";
            mes "那這次失敗也沒關係了.";
            close;
        }
        specialeffect2 155;
        failedrefitem .@RItem;
        mes .npcname$;
        mes "抱歉您的物品精煉失敗了!";
        close;
    }
}

//====================================================//

function	script	Repair	{

	mes .npcname$;
	mes"請選擇您要修理的物品選項。";
	next;
	if ( getbrokenid(1) == NULL ) {	mes .npcname$;
					mes "您沒有任何受損的物品唷!";
					close;	}

	set .@RepItem,select(getitemname(getbrokenid(1)),getitemname(getbrokenid(2)),
				getitemname(getbrokenid(3)),getitemname(getbrokenid(4)),
				getitemname(getbrokenid(5)),getitemname(getbrokenid(6)),
				getitemname(getbrokenid(7)),getitemname(getbrokenid(8)),
				getitemname(getbrokenid(9)),getitemname(getbrokenid(10)));

	if ( getbrokenid(.@RepItem) == NULL ) {	close;	}
	mes .npcname$;
	mes "修理 ^0000FF"+getitemname(getbrokenid(.@RepItem))+"^000000 這項物品";
	mes "需要 ^FF0000鋼鐵     1^000000 個";
	mes "　和 ^FF0000金錢 10000^000000 z";
	next;
	if (select("是的!","不了!")==2) {
					close;
					}
	if ( countitem(999) < 1 || Zeny < 10000 ) {	mes .npcname$;
							mes "抱歉您身上的物品不足!";
							close;	}
	delitem 999,1;
	set Zeny,Zeny - 10000;
	repair(.@RepItem);
	mes .npcname$;
	mes"鏘！鏘！鏘！";
	next;
	mes .npcname$;
	mes"恭喜您物品已修理完成了!";
	close;
}

//==================================================//

function	script	CardMain	{

	mes .npcname$;
	mes"請選擇您要拆除的物品選項。";
	next;
	set .@CardPart,select(getequipname(1),getequipname(2),getequipname(3),getequipname(4),
				getequipname(5),getequipname(6),getequipname(7),getequipname(8),
				getequipname(9),getequipname(10));

	if ( getequipcardcnt(.@CardPart) == 0 )	{	mes .npcname$;
							mes "這個物品並沒有插上卡片唷!";
							close;	}
	set .@CardCount,getequipcardcnt(.@CardPart);
	mes .npcname$;
	mes "拆除 ^0000FF"+getequipname(.@CardPart)+"^000000 上的卡片，";
	mes "需要 ^FF0000金錢 "+(1000000+(.@CardCount*250000))+"^000000 z";
	mes "　和 ^FF0000鋼鐵　　5^000000 個";
	next;
	if (select("是的!","不了!")==2) {
					close;
					}
	if ( zeny < (1000000+(.@CardCount*250000)) || 
				countitem(999) < 5  ) {	mes .npcname$;
								mes "抱歉您身上的物品不足!";
								close;	}
	delitem 999,5;
	set zeny,zeny - (1000000+(.@CardCount*250000));
	successremovecards .@CardPart;
	mes .npcname$;
	mes"鏘！鏘！鏘！";
	next;
	mes .npcname$;
	mes"恭喜您拆除卡片已完成了!";
	close;
}

//==================================================//

function	script	ShopMain	{

	mes .npcname$;
	mes"請選擇您要購買的物品選項。";
	next;
	switch(select("鍛治鐵砧","鍛治物品","強化金屬")) {
case 1:
	switch(select("鐵砧","神之金屬鐵砧","黃金鐵砧")) {
	case 1:	set .@SItem,986;	set .@SZeny,3000;	goto Shop_Item;
	case 2:	set .@SItem,987;	set .@SZeny,120000;	goto Shop_Item;
	case 3:	set .@SItem,988;	set .@SZeny,300000;	goto Shop_Item;	}
case 2:
	switch(select("攜帶用熔礦爐","鋼鐵錘子","黃金錘子","神之金屬錘子")) {
	case 1:	set .@SItem,612;	set .@SZeny,150;	goto Shop_Item;
	case 2:	set .@SItem,613;	set .@SZeny,1000;	goto Shop_Item;
	case 3:	set .@SItem,614;	set .@SZeny,3000;	goto Shop_Item;
	case 4:	set .@SItem,615;	set .@SZeny,5000;	goto Shop_Item;	}
case 3:
	switch(select("強化武器金屬-級數一","強化武器金屬-級數二")) {
	case 1:	set .@SItem,1010;	set .@SZeny,200;	goto Shop_Item;
	case 2:	set .@SItem,1011;	set .@SZeny,1000;	goto Shop_Item;	}	}

Shop_Item:
	mes .npcname$;
	mes "購買 ^0000FF"+getitemname(.@SItem)+"^000000 這項物品，";
	mes "需要 ^FF0000金錢 "+.@SZeny+"^000000 z";
	mes "　";
	mes "數量：Min 1 個，Max 100 個！";
	next;
	input .@ItemNum;
	if ( .@ItemNum < 1  || .@ItemNum > 100 ) {	mes .npcname$;
							mes "您輸入的數量不正確唷!";
							close;	}
	mes "[阿呆工匠][^0000FF計算機^000000]";
	mes "購買 ^0000FF"+getitemname(.@SItem)+"^000000 ^0000FF"+.@ItemNum+"^000000 個，";
	mes "需要 ^FF0000金錢 "+(.@SZeny*.@ItemNum)+"^000000 z";
	mes "　";
	mes"計算完畢！請問您還要繼續嗎？";
	next;
	switch(select("是的!","不了!","重設數量","返回選單")) {
	case 1:	break;
	case 2:	close;
	case 3:	goto Shop_Item;
	case 4:	callfunc "ShopMain";	end;	}
	if ( zeny < (.@SZeny*.@ItemNum) ) {		mes .npcname$;
							mes "抱歉您身上的金錢不足!";
							close;	}
	if( checkweight(.@SItem,.@ItemNum)==0 ) {	mes .npcname$;
							mes "抱歉您身上的空間不足!";
							close;	}
	set Zeny,Zeny - (.@SZeny*.@ItemNum);
	getitem .@SItem,.@ItemNum;
	mes .npcname$;
	mes"鏘！鏘！鏘！";
	next;
	mes .npcname$;
	mes"恭喜您選購材料已完成了!";
	close;
}

//==================================================//

function	script	Change	{
	mes .npcname$;
        mes "^0088FF"+strcharinfo(0)+"^000000您好。";
	mes "請選擇要兌換的選項";
	next;
	switch(select("兌換 ^9933FF神之金屬^000000","兌換 ^9933FF鋁^000000"))
	{
	case 1 : setarray .@data,984,756; break;
	case 2 : setarray .@data,985,757; break;
		}
	mes .npcname$;
	mes "哦,你要換^9933FF"+getitemname(.@data[0])+"^000000阿";
	mes "^993333**材料如下**^000000";
	mes "[^0000FF"+ getitemname(.@data[1])+"^000000] ^FF00005個^000000";
	next;
	mes .npcname$;
	mes "^9933335個"+ getitemname(.@data[1]) +"可換1個"+ getitemname(.@data[0]) +"^000000";
	mes "那麼,你要換多少個^993333"+ getitemname(.@data[0])+"^000000?";
	mes"[^0000FF"+ getitemname(.@data[1]) +"^000000有^FF0000"+countitem(.@data[1])+"^000000個]";
	mes "^880000(輸入0取消)^000000";	
	next;
	input .@n;
	if(.@n == 0) close;
	if((.@n*5) > countitem(.@data[1]))
		{
		mes .npcname$;
		mes "^FF0000"+.@n+"^000000個?數量不對喔";
		close;
		}
	switch(select("不換","換"))
	{
	case 1: close; break;
	case 2: 
	delitem .@data[1],(.@n*5);
	getitem .@data[0],.@n;
		}
	close;
}

function	script	GetRefineSelectMenu	{
    for (set .@i, 0; .@i < getarraysize($@TRANS_SELECT_TEXT$); set .@i, .@i + 1) {
        set .@menu$, .@menu$ + "^FF0088[" + $@TRANS_SELECT_TEXT$[.@i] + "] - ^000000" + (getequipname($@TRANS_SELECT_VALUE[.@i]) == "" ? "沒有裝備" : getequipname($@TRANS_SELECT_VALUE[.@i])) + ":";
    }

    return .@menu$;
}