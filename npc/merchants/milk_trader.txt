//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================

//===== rAthena Script =======================================
//= Milk Trader
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Trades bottles for milk
//===== Additional Comments: =================================
//= Fully working
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Raised the price to close zeny exploit [Lupus]
//= 1.2a Switched to Lupus's "loopless" technique.[kobra_k88]
//= 1.3 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.4 Implemented checkweight. [L0ne_W0lf]
//= 1.5 Fixed missed variable. (bugreport:1523) [L0ne_W0lf]
//============================================================

prontera,73,140,0	script	牛奶供應商	86,{
	if (checkweight(1201,1) == 0) {
		mes "[牛奶供應商]";
		mes "我無法為你提供服務了，";
		mes "因為你身上攜帶的東西太多了。";
		mes "請將暫時不用的東西存放到卡普拉倉庫之後，再來找我吧~";
		close;
	}
	mes "[牛奶供應商]";
	mes "嗨, 嗨...";
	mes "如果你給我帶來 ^0000FF1個空瓶^000000 和 ^FF000015金幣^000000，";
	mes "那麼我會幫你將空瓶裝滿牛奶。";
	mes "怎麼樣？要不要試一下？";
	next;
	if (select("將身上所有空瓶裝滿成牛奶:取消") == 1) {
		if (countitem(713) <= 0) {
			mes "[牛奶供應商]";
			mes "囧...你好像一個空瓶都沒有呢。";
			mes "我真的沒辦法給你製作成牛奶，";
			mes "你懂的...";
			close;
		}
		set .@bottles,countitem(713);
		set .@total_weight,.@bottles * 50;
		set .@total_cost,.@bottles * 15;
		if (Zeny < .@Total_cost) {
			mes "[牛奶供應商]";
			mes "噢, 我汗~!";
			mes "你沒有足夠的金錢來呢，想將身上的空瓶全都裝滿牛奶需要" + .@total_cost + " 金幣。";
			close;
		}
		if (MaxWeight - Weight < .@total_weight) {
			mes "[牛奶供應商]";
			mes "唔...";
			mes "再多騰出一點背包的空間吧。";
			mes "否則裝這麼多牛奶的話，你會背不動的。";
			close;
		}
		set Zeny, Zeny-.@total_cost;
		delitem 713,.@bottles; //Empty Bottles
		getitem 519,.@bottles; //Milk
		close;
	}
	close;
}
