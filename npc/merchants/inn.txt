//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================

//===== rAthena Script =======================================
//= Inn Npcs
//===== By: ==================================================
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Current Version: =====================================
//= 2.9a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Inn Npcs, Save and Heal
//============================================================
//= variables:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to save at
//= arg3, arg4 - x and y cord. to save at
//= @cost - cost of renting a room
//===== Additional Comments: =================================
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Inn Maid. Official warp
//=     and save points[erKURITA]
//= 1.4 Added Hugel inn [erKurita]
//= 1.5 Added "end;" after warping the player. [L0ne_W0lf]
//=	- Small corrections to the prontera Inns.
//=	- Removed the Cancel dialog.
//= 1.6 More fixes, changed progression of "Rest"ing.
//=	- Corrected Morroc NPCs. [L0ne_W0lf]
//= 1.7 Corrected Payon NPC. [L0ne_W0lf] 
//= 1.8 Corrected Geffen NPC. [L0ne_W0lf] 
//= 1.9 Added the Inn for Al De baran. [L0ne_W0lf]
//= 2.0 Added Inn for the Town of Veins. [L0ne_W0lf]
//= 2.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.1a Corrected a little Typo error. (bugreport:536 & 537) [Samuray22]
//= 2.2 Small update to Vein's Inn Master. [L0ne_W0lf]
//= 2.3 Added Lighthalzen Inn from Lighthalzen town file. [L0ne_W0lf]
//= 2.4 Fixed Inn Employee#Ahlma warping to same destination as save point, bugreport: 191.
//= 2.5 Added Moskovia Gostinitsa (Inn) from jA
//= 2.6 Updated Alberta inn to official. [L0ne_W0lf]
//= 2.7 Removed Morroc Inns as per episode 12.1. [L0ne_W0lf]
//= 2.8 Removed Moscovia Inn; Its in quests/quest_moscovia.txt [Kisuka]
//= 2.9 Added switch menus, updated to match AEGIS scripts. [Kisuka]
//= 2.9a Added 'npcskill' command. [Euphy]
//============================================================


//======================== Prontera ===================================
// West Side Inn -------------------
prt_in,244,135,2	script	旅館 服務生#Sammy	53,{

	callfunc "F_InnMaid","[服務生 善美]","內卡拉司旅館","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// East Side Inn ------------------
prt_in,61,141,2	script	旅館 服務生#Ahlma	53,{

	callfunc "F_InnMaid","[服務生 阿瑪]","內卡拉司旅館","prt_in",64,136;
	warp "prt_in",60,166; end;
}

//======================== Alberta ====================================
alberta_in,32,142,3	script	服務員 珍妮#Jennie	53,{

	callfunc "F_InnMaid","[服務員 珍妮]","琵秀旅館","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//====================== Geffen ======================================
geffen_in,70,64,5	script	旅館 服務生#Cena	53,{

	callfunc "F_InnMaid","[服務生 賽娜]","伊芙麗特旅館","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}

//======================= Payon ======================================
payon_in01,132,62,5	script	旅館 服務生#Ahee	53,{

	callfunc "F_InnMaid","[服務生 阿喜]","斐揚旅館","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//======================== Morocc ====================================
// North East --------------------------
/*
morocc_in,147,141,3	script	旅館 服務生#Hasna	53,{

	callfunc "F_InnMaid","[服務生 漢斯娜]","夢羅克旅館","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// South --------------------------------
morocc_in,80,100,5	script	旅館 服務生#Manar	53,{

	callfunc "F_InnMaid","[服務生 馬納爾]","夢羅克旅館","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}
*/

//====================== Aldebaran ===================================
aldeba_in,92,58,5	script	旅館 服務生#Rilim	53,{

	callfunc "F_InnMaid","[服務生 立琳]","艾爾帕蘭旅館","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//======================== Rachel ====================================
ra_in01,376,69,4	script	飯店 職員#Annie	931,{

	callfunc "F_InnMaid","[飯店 職員]","拉赫飯店","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//====================== Lighthalzen =================================
lhz_in02,230,284,4	script	飯店 職員#01	86,{
	mes "[飯店 職員]";
	mes "歡迎光臨，我們是業界最高級的飯店。";
	mes "叫做「皇龍」飯店。";
	mes "我們擁有寬敞的空間，";
	mes "提供最舒適的休息環境，";
	mes "讓顧客賓至如歸，保證滿意！";
	next;
	switch(select("儲存服務:休息 - 5,000 金幣:結束對話")) {
	case 1:
		savepoint "lhz_in02",209,275,1,1;
		mes "[飯店 職員]";
		mes "您的復活點已經存儲在皇龍飯店啦。";
		mes "十分感謝您對我們的信任。";
		close;
	case 2:
		if(Zeny < 5000) {
			mes "[飯店 職員]";
			mes "很抱歉，服務費需要5,000金幣。";
			mes "請確認你有足夠的金幣後再來找我，好嗎？";
			close;
		}
		set Zeny,Zeny - 5000;
		mes "[飯店 職員]";
		mes "對您致以最誠摯的謝意~";
		mes "希望您能享受「皇龍」給您帶來的休息時光。";
		close2;
		warp "lhz_in02",219,150;
		percentheal 100,100;
		npcskill "AL_BLESSING",10,99,99;
		end;
	case 3:
		mes "[飯店 職員]";
		mes "感謝您的光臨，希望你能過得愉快。";
		close;
	}
}

//======================== Hugel ====================================
hu_in01,246,107,3	script	旅館 員工#Receptionist	53,{

	callfunc "F_InnMaid","[接待員]","毀葛旅館","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//======================== Veins ====================================
ve_in,157,219,5	script	旅館 老闆#Receptionist	709,{
	mes "[旅館 老闆]";
	mes "歡迎光臨自由自在的菲音斯旅館~";
	next;
	switch(select("儲存服務:休息 -> 5000 金幣:結束對話")) {
	case 1:
		mes "[旅館 老闆]";
		mes "您的復活點已經存儲在菲音斯。";
		mes "盡情享受這個城鎮帶來的風光吧~";
		savepoint "ve_in",157,209,1,1;
		close;
	case 2:
		mes "[旅館 老闆]";
		if(Zeny < 5000){
			mes "很抱歉，服務費需要5,000金幣。";
			mes "請確認你有足夠的金幣後再來找我，好嗎？";
			close;
		}
		mes "放滿腳步，多在這個城鎮停留一下~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		warp "ve_in",184,228;
		end;
	case 3:
		mes "[旅館 老闆]";
		mes "歡迎下次光臨~";
		close;
	}
}

//======================= Inn Function ==============================
function	script	F_InnMaid	{
	.@npc_name$ = getarg(0);

	mes .@npc_name$;
	mes "歡迎光臨" + getarg(1) + "！";
	mes "請問有什麼需要我幫忙的嘛？";
	next;
	switch(select("儲存服務:休息 -> 5000 金幣:結束對話")) {
	case 1:
		mes .@npc_name$;
		mes "您的復活點已經存儲完畢。";
		mes "歡迎您下次光臨。";
		savepoint getarg(2),getarg(3),getarg(4),1,1;
		close;
	case 2:
		mes .@npc_name$;
		if(Zeny < 5000){
			mes "很抱歉，服務費需要5,000金幣。";
			mes "請確認你有足夠的金幣後再來找我，好嗎？";
			close;
		}
		mes "謝謝惠顧。";
		mes "希望您能好好休息~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		return;
	case 3:
		close;
	}
}
