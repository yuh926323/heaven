//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================

//===== rAthena Script =======================================
//= Juice Maker Quest
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 2.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Learn where to turn apples, bananas, carrots, and grapes
//= into juice.
//= Condition if done: (MISC_QUEST & 1)
//===== Additional Comments: =================================
//= 1.9 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     Added tracking variables in, which are unset when complete.
//= 2.0 Added missing checkweight. [L0ne_W0lf]
//= 2.1 Fixed dialog for if your missing the needed items (He always said the dialog for grape juice). [Kisuka]
//============================================================

prt_in,49,172,3	script	瑪麗亞稜#juice	53,{
	if ((MISC_QUEST&1) || morison_meat == 15) {
		mes "[家庭主婦 瑪麗亞稜]";
		mes "哇哦...!";
		mes "不過他除了它的肉什麼都不吃. 如果水果被切開，他也許也會吃. 比如~把水果加工成果汁~";
		next;
		set mother_marienu,0;
		set morison_meat,0;
		set MISC_QUEST,MISC_QUEST | 1;
		mes "[家庭主婦 瑪麗亞稜]";
		mes "噢~,我想想看,我曾經聽說過製作果汁的地方。現在在哪裡呢！夢羅克還是斐揚？";
		close;
	}
	if (mother_marienu == 1) {
		mes "[家庭主婦 瑪麗亞稜]";
		mes "莫裡森，多吃些水果！你不想當你長大的時候變成一個肥豬吧？你說呢？";
		next;
		if (select("繼續交談:取消") == 1) {
			mes "[家庭主婦 瑪麗亞稜]";
			mes "咦，，你！";
			mes "這塊肉是誰給你的, 你來這裡以為我不知道嗎？";
			next;
			mes "[家庭主婦 瑪麗亞稜]";
			mes "出去！走離開我的家！!!";
			close;
		}
		mes "[家庭主婦 瑪麗亞稜]";
		mes "莫裡森！如果你繼續這麼做，我會很生氣！";
		close;
	}
	if (morison_meat > 0) {
		set mother_marienu,1;
		mes "[家庭主婦 瑪麗亞稜]";
		mes "哦！。。這....";
		mes "這是什麼？......";
		mes "你能為我的寶貝做什麼嗎!";
		next;
		mes "[家庭主婦 瑪麗亞稜]";
		mes "啊！趕快從我眼前消失！";
		close;
	}
	mes "[家庭主婦 瑪麗亞稜]";
	mes "莫裡森！求你了！吃些水果吧！求你了！";
	next;
	mes "[家庭主婦 瑪麗亞稜]";
	mes "唉，和你爸爸一副德行...";
	next;
	switch(select("繼續交談:取消")) {
		mes "[家庭主婦 瑪麗亞稜]";
		mes "*歎氣*... 這就是莫裡森...";
		mes "我唯一的兒子。";
		next;
		mes "[家庭主婦 瑪麗亞稜]";
		mes "但是這幾天，他不吃水果了，他臉色那麼的淡。因為他飲食不健康。他只知道吃肉，我要找到一個方法~給我的寶貝吃下水果和蔬菜...";
		close;
	}
	mes "[家庭主婦 瑪麗亞稜]";
	mes "莫裡森，如果你保持這樣你會有大麻煩！";
	close;
}

prt_in,47,173,3	script	莫利孫#juice	97,{
	if ((MISC_QUEST&1) || morison_meat == 15) {
		mes "[小孩 莫利孫]";
		mes "噗...別在意。";
		mes "我不要吃那難吃的水果，如果只是吃水果，我會慢慢的變得營養不良，走向死亡..... 不要為我擔心 .";
		close;
	}
	if (morison_meat > 9) {
		mes "[小孩 莫利孫]";
		mes "哇~好飽啊~";
		mes "我想我現在可以走了~";
		mes "如果我再吃我感覺我快要爆炸了";
		if (morison_meat == 10) {
			next;
			set morison_meat,morison_meat+1;
			mes "[小孩 莫利孫]";
			mes "啊~~嗯嗯.";
			mes "拿著這些~";
			next;
			mes "[小孩 莫利孫]";
			mes "我有一些小積蓄, 自從你給我吃肉，我就放棄了零食。.";
			close;
			getitem 529,3; //Candy
			getitem 530,1; //Candy_Striper
		}
		close;
	}
	mes "[小孩 莫利孫]";
	mes "啊！！不!!!";
	mes "不！不要讓我吃水果！我不能忍受水果妖怪！";
	next;
	if (countitem(517) > 0) {
		switch(select("交談:給他看肉:結束對話")) {
		case 1:
			mes "[小孩 莫利孫]";
			mes "啊！一次或兩次就夠了！！我拒絕吃更多的水果！你要剝他們的皮有太多的果汁，這會讓你感到噁心……";
			mes "[小孩 莫利孫]";
			mes "他們有的滑滑的，酸酸的其實吃了也還是會感到餓，給我肉吧~";
			close;
		case 2:
			mes "[小孩 莫利孫]";
			mes "噢！這...這是肉~";
			mes "嗯...嗯嗯...肉的氣味真香啊...";
			mes "打攪一下...";
			mes "我...能給我一個嗎?";
			next;
			if (select("給他肉:不給他肉") == 1) {
				delitem 517,1; //Meat
				set morison_meat,morison_meat+1;
				mes "[小孩 莫利孫]";
				mes "哇哦~！！！肉！！";
				mes "太棒了！！";
				mes "謝謝你，我想我可以離開了。";
				mes "*嚼 嚼*";
				close;
			}
			set morison_meat,15;
			mes "[小孩 莫利孫]";
			mes "哇...！";
			mes "我現在明白了。";
			mes "大人們都是一樣壞！";
			close;
		case 3:
			mes "[小孩 莫利孫]";
			mes "啊啊啊！不管怎麼樣我都不會吃水果蔬菜的！";
			close;
		}
	}
	if (select("繼續交談:結束對話") == 1) {
		mes "[小孩 莫利孫]";
		mes "啊啊啊！！我再也不吃水果了！";
		next;
		mes "[小孩 莫利孫]";
		mes "我不吃水果了！剝開有那麼多果汁流出來，他們使我感到噁心！";
		next;
		mes "[小孩 莫利孫]";
		mes "他們都很滑很酸，即使我吃了，我還是餓.";
		mes "啊！";
		mes "給我肉！!";
		close;
	}
	mes "[小孩 莫利孫]";
	mes "啊啊啊啊！我不吃蔬菜！！水果也不吃！！嗚嗚";
	close;
}

payon_in03,188,146,5	script	馬雷克漢森#juice	86,{
	if (checkweight(1201,1) == 0) {
		mes "- 稍等！-";
		mes "- 目前你現在帶的東西太多了 -";
		mes "- 可以把它們放進倉庫 -";
		mes "- 然後再回來找我哦~. -";
		close;
	}
	if (MISC_QUEST&1) {
		mes "[商人 馬雷克漢森]";
		mes "喲.";
		mes "你想找本大爺製作果汁？";
		next;
		switch(select("我要製作果汁！:瞭解製作果汁的材料:取消")) {
		case 1:
			mes "[商人 馬雷克漢森]";
			mes "你想製作什麼類型的果汁？";
			next;
			switch(select("蘋果汁:香蕉汁:紅蘿蔔汁:葡萄汁:取消")) {
			case 1:
				set .@fruit,512;
				set .@juice,531;
				break;
			case 2:
				set .@fruit,513;
				set .@juice,532;
				break;
			case 3:
				set .@fruit,515;
				set .@juice,534;
				break;
			case 4:
				set .@fruit,514;
				set .@juice,533;
				break;
			case 5:
				mes "[商人 馬雷克漢森]";
				mes "嗯，有空常來哈~";
				close;
			}
			if (countitem(.@fruit) == 0 || countitem(713) == 0 || Zeny < 3) {
				mes "[商人 馬雷克漢森]";
				mes "噢，額滴神啊...";
				mes "小子，你的材料不夠呀。";
				mes "想製作 "+getitemname(.@juice)+" 的話，";
				mes "我需要 1 個 "+getitemname(.@fruit)+" 和 1 個空瓶。";
				mes "此外還要 3 Zeny 的手續費。";
				next;
				mes "[商人 馬雷克漢森]";
				mes "等你準備好材料再來找我吧， 我給你製作美味的果♂汁。";
				close;
			}

			mes "[商人 馬雷克漢森]";
			mes "你要製作多少啊？";
			next;
			switch(select("我的材料能製作多少個，就要多少個:我想自己決定製作的數量:取消")) {
			case 1:
				set .@make,countitem(.@fruit);
				if (countitem(713) < .@make) set .@make, countitem(713);
				if (Zeny/3 < .@make) set .@make, Zeny/3;
				break;
			case 2:
				mes "[商人 馬雷克漢森]";
				mes "選擇的數字要小於 100 如果你想取消，";
				mes "請輸入 '0' 即可。";
				mes "你的材料最多可以製作 " + countitem(.@fruit) + " 瓶果汁。";
				next;
				while(1) {
					input .@input;
					if (.@input == 0) {
						mes "[商人 馬雷克漢森]";
						mes "嗯，有空再來...";
						close;
					}
					else if (.@input > 100) {
						mes "[商人 馬雷克漢森]";
						mes "要我一次製作100瓶不可能的啦，最多99個一次！";
						next;
					}
					else {
						break;
					}
				}
				set .@make,.@input;
				break;
			case 3:
				mes "[商人 馬雷克漢森]";
				mes "嗯，有空再來...";
				close;
			}

			set .@total_zeny,3 * .@make;

			if (countitem(.@fruit) < .@make || countitem(713) < .@make || Zeny < .@total_zeny) {
				mes "[商人 馬雷克漢森]";
				mes "小子，你的材料不夠呀，等你準備好材料再來找我吧。";
				close;
			}
			delitem .@fruit,.@make;
			delitem 713,.@make;
			set Zeny, Zeny-.@total_zeny;
			getitem .@juice,.@make;

			mes "[商人 馬雷克漢森]";
			mes "喏，拿去吧！清爽可口的果汁！";
			next;
			mes "[商人 馬雷克漢森]";
			mes "嗯，有空再來...";
			close;
		case 2:
			mes "[商人 馬雷克漢森]";
			mes "人類之所以有今天的發展，是因為我們獲得了大自然的祝福——果實.";
			next;
			mes "[商人 馬雷克漢森]";
			mes "如今生命如此繁榮，年輕的一代卻不怎麼吃水果，於是本大爺就想怎麼讓水果更受歡迎。";
			next;
			mes "[商人 馬雷克漢森]";
			mes "我意識到用水果製作的果汁讓年輕人更容易接受。";
			next;
			mes "# 果汁製作材料清單 #";
			mes "^CC4E5C- 蘋果汁 -^000000";
			mes "蘋果 x 1 個，空瓶 x 1 個，3 金幣";
			mes "^E3CF57- 香蕉汁 -^000000";
			mes "香蕉 x 1 條，空瓶 x 1 個，3 金幣";
			mes "^ED9121- 蘿蔔汁 -^000000";
			mes "紅蘿蔔 x 1 個，空瓶 x 1 個，3 金幣";
			mes "^CC00FF- 葡萄汁 -^000000";
			mes "葡萄 x 1 顆，空瓶 x 1 個，3 金幣";
			close;
		case 3:
			mes "[商人 馬雷克漢森]";
			mes "小子，有空幫我宣傳一下啊！";
			close;
		}
	}
	else {
		mes "[商人 馬雷克漢森]";
		mes "喲！";
		mes "你想找本大爺製作果汁？";
		next;
		if (select("對話:取消") == 1) {
			mes "[商人 馬雷克漢森]";
			mes "人類之所以有今天的發展，是因為我們獲得了大自然的祝福——果實。";
			next;
			mes "[商人 馬雷克漢森]";
			mes "如今生命如此繁榮，年輕的一代卻不怎麼吃水果，於是本大爺就想怎麼讓水果更受歡迎。";
			next;
			mes "[商人 馬雷克漢森]";
			mes "我意識到用水果製作的果汁讓年輕人更容易接受。";
			close;
		}
		mes "[商人 馬雷克漢森]";
		mes "小子，有空幫我宣傳一下啊！";
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.1 fixed input number check [Lupus]
//= 1.2 fixed misplaced Grape<->Carrot juices [Lupus]
//= 1.3 added a loopless menu 'as many as possible' [Lupus]
//= 1.3a Turned the juice making part of the Juicer npc into a
//=     subfunc.  Changed fruit amount needed to 1.[kobra_k88]
//=     Fixed exploits [Lupus]
//= 1.6 fixed typo (where did @ipnut came from?) [Lance]
//= 1.8 got rid of vars: 'MEAT',"MARIANNE","MORRISON"
//=     From now, all common quests start using bits of the
//=     same variable MISC_QUEST [Lupus]
//============================================================ 
