//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================

//===== rAthena Script =======================================
//= Archer Skill Quest
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Quest for skills: Arrow Crafting, Arrow Repel
//===== Additional Comments: ================================= 
//= 1.5 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//= 1.6 Updated to latest available official file.
//=     also fixed Item issue [bugreport:5607]. [Masao]
//= 1.7 Fixed JobLevel requirements. [Euphy]
//============================================================

moc_ruins,118,99,5	script	爵 納利	88,{
	if (BaseClass == Job_Archer) {
		if (getskilllv("AC_MAKINGARROW") == 1) {
			mes "[爵 納利]";
			mes "噢噢, 是我家鄉人啊!";
			mes "很高興見到你!";
			mes "過的還不錯吧?";
			mes "啊! 這箭矢!";
			mes "是自己做的啊?";
			next;
			mes "[爵 納利]";
			mes "哈哈...!";
			mes "做的不錯嗎?";
			mes "哈哈... 不管怎麼樣, 我已經很高興了";
			mes "下次找我的時候, 也多帶點家鄉的消息來吧.";
			mes "那麼再見囉~";
			close;
		}
		else if (JobLevel >= 30 || (BaseJob == Job_Hunter || BaseJob == Job_Bard || BaseJob == Job_Dancer)) {
			mes "[爵 納利]";
			mes "啊!"; 
			mes " ";
			mes "你是...";
			next;
			if ((countitem(907) > 19) && (countitem(921) > 6) && (countitem(906) > 40) && (countitem(1019) > 12) && (countitem(501) > 0)) {
				mes "[爵 納利]";
				mes "你把東西帶來了!"; 
				mes "非常謝謝你.";
				mes "那就按照約定, 我來教你技能吧.";
				next;
				delitem 907,20; //Resin
				delitem 921,7; //Mushroom_Spore
				delitem 906,41; //Tough_Scalelike_Stem
				delitem 1019,13; //Wooden_Block
				delitem 501,1; //Red_Potion
				skill "AC_MAKINGARROW",1,SKILL_PERM;
				mes "[爵 納利]";
				mes "那麼, 從現在開始, 就不用擔心箭矢了";
				mes "對啦, 你在斐揚的時候, 有沒有看到";
				mes "葛豪這個傢伙啊?";
				mes "請你小心啊. 那傢伙是";
				mes "非常殘暴的傢伙啊";
				next;
				mes "[爵 納利]";
				mes "去斐揚的時候, 記得小心葛豪就好";
				mes "請你記得.";
				mes "那就再見囉~";
				close;
			}
			mes "[爵 納利]";
			mes "在夢羅克使用弓箭的人吶!?";
			mes "很高興見到你! ";
			mes "在這種地方見到同胞! 真是太高興了!";
			mes "我獨自一人來到夢羅克..";
			mes "這裡很排斥外人... 嗚嗚嗚~";
			mes "我很孤獨哩~";
			next;
			switch (select("肯定很難過吧.還好嗎?:繼續加油啊")) {
			case 1:
				mes "[爵 納利]";
				mes "是的. 謝謝你...";
				mes "你在這外地也挺的辛苦的吧.";
				mes "找到箭矢非常難?";
				mes "我都自己動手做箭矢的哩.";
				next;
				select("啊,真的嗎?!");
				mes "[爵 納利]";
				mes "是的! 我把雜項物品, 搜集起來之後";
				mes "就可以拿來製作箭矢.";
				mes "活在這辛苦的世界";
				mes "算是一個不錯的手藝啊.";
				mes "若你想學的話, 我可以教你啊.";
				next;
				select("那就拜託你了");
				mes "[爵 納利]";
				mes "不過可不是免費呀.";
				mes "天下沒有白吃的午餐啊~";
				mes "嗯... 那就這樣好了?"; 
				mes "請你幫我尋獲, 我所指定的物品的話.";
				mes "我就教你這個技能.";
				next;
				mes "[爵 納利]";
				mes "我離開故鄉之後, 感覺到非常孤單.";
				mes "想藉著故鄉的物品";
				mes "來安慰我孤單的心情啊.";
				mes "我需要在斐揚森林中,";
				mes "從橡膠樹上流下來的枯木汁20個.";
				mes "在斐揚商店出售的紅色藥水1瓶";
				mes "請你拿給我好嗎."; 
				next;
				mes "[爵 納利]";
				mes "還需要, 活在斐揚森林附近";
				mes "從樹精魔物身上掉下來的, 瑪哪樹枝13個";
				mes "森靈的鱗片梗41個";
				mes "毒魔菇的香菇芽孢7個";
				mes "你可以拿給我嗎";
				next;
				mes "[爵 納利]";
				mes "等到到時後, 拿到之後我再教你技能好了";
				mes "那... 我就等你囉.";
				mes "等待你帶來家鄉的消息.";
				close;
			case 2:
				mes "[爵 納利]";
				mes "...嘿 "+((Sex == SEX_MALE)?"先生":"小姐")+".";
				mes "...晚上走路小心啊.";
				close;
			}
		}
		mes "[爵 納利]";
		mes "嗯... 什麼?";
		mes "還有話要說嗎?";
		mes "我可沒有話要說啊.";
		mes "有一位賢能者, 把我的對話給中斷了.";
		close;
	}
	mes "[?]";
	mes "什麼... 第一次看到弓箭手嗎?";
	mes "繼續走你的路吧.";
	mes "若是沒碰到, 有水準的弓箭手的話"; 
	mes "我是不想開口的人啊!";
	close;
}

payon,103,63,3	script	葛豪	88,3,3,{
	if (BaseClass == Job_Archer) {
		if (getskilllv("AC_CHARGEARROW") == 1) {
			mes "[葛豪]";
			mes "我們又見面了.還真奇怪啊."; 
			mes "為什麼我見到, 曾經見過的人";
			mes "就會有撒嬌的口語出現呢."; 
			mes "啊... 不管怎麼樣, 很高興又見到你.";
			mes "啊... 以後再不要來喔, 啊啊...";
			close;
		}
		else if (JobLevel >= 35 || (BaseJob == Job_Hunter || BaseJob == Job_Bard || BaseJob == Job_Dancer)) {
			mes "[葛豪]";
			mes "真是的... 受傷的地方很不容易好啊.";
			mes "我變成這副德行... 都是我太大意啦. 唉...";
			mes "雖然是這樣, 可是還給我造成";
			mes "這麼大的傷勢...";
			next;
			mes "爵 納利, 這傢伙要怎麼辦.";
			mes "恩嗯... 啊!";
			mes "你? 什麼時候待在那裡的啊?"; 
			mes "嗯... 真是高手啊.";
			mes "像你這樣的高手";
			mes "當然一定會使用衝鋒箭囉.";
			next;
			switch(select("請問那是什麼啊?:現在可以教我嗎?")) {
			case 1:
				mes "[葛豪]";
				mes "...你是在說笑啊?"; 
				mes "你已經到這樣子的等級了";
				mes "還沒有修練到衝鋒箭啊?";
				mes "真是特別的人啊.";
				next;
				mes "[葛豪]";
				mes "(葛豪開始進入發呆的狀態)";
				next;
				mes "[葛豪]";
				mes "好吧, 就這樣子吧. 我來教你";
				mes "什麼叫做衝鋒箭.";
				next;
				mes "[葛豪]";
				mes "所謂衝鋒箭是指, 當攻擊的同時";
				mes "把敵人往後退移的技能";
				mes "稍為花點時間瞄準, 才能使用";
				mes "和魔法不一樣的是"; 
				mes "施法時間不會被中斷.";
				next;
				mes "[葛豪]";
				mes "在近距離會給較大的傷害力";
				mes "對弓箭手來說, 是一個不錯的技能."; 
				mes "若你想學的話, 我可以教你啊.";
				next;
				mes "[葛豪]";
				mes "首先要改造弓";
				mes "請你尋找一個不用的十字弓給我";
				mes "還有觸鬚 10 個 和鳥嘴 10 個";
				mes "猴子尾巴 3 個, 這彈力不錯.";
				mes "還有綠寶石 2 個. 最後還需要...";
				mes "我最喜歡的香蕉汁36瓶!";
				next;
				mes "[葛豪]";
				mes "......咳咳!";
				mes "這樣子準備好之後, 給我的話";
				mes "我會傳授給你衝鋒箭技能.";
				mes "那就, 請你出發吧.";
				mes "(假如沒有帶香蕉汁的話, 我要痛扁他.)";
				close;
			case 2:
				if ((countitem(721) > 1) && (countitem(942) > 2) && (countitem(962) > 9) && (countitem(925) > 9) && (countitem(532) > 35)) {
					mes "[葛豪]";
					mes "不錯! 很標準!";
					mes "我要把全套的弓箭技能";
					mes "衝鋒箭傳授給你!";
					next;
					delitem 721,2; //Azure_Jewel
					delitem 942,3; //Yoyo_Tail
					delitem 962,10; //Tentacle
					delitem 925,10; //Bill_Of_Birds
					delitem 532,36; //Banana_Juice
					skill "AC_CHARGEARROW",1,SKILL_PERM;
					mes "[葛豪]";
					mes "噢, 比我想像中做的更好!";
					mes "更不需要改良弓了!"; 
					mes "這請你再帶回去吧~";
					mes "從現在開始, 請你盡情的享受";
					mes "在洞穴和原野上的表現了!";
					mes "哈~哈~!";
					close;
				}
				mes "[葛豪]";
				mes "嗯... 有點可惜啊.";
				mes "有個地方不足啊.";
				mes "我在重新說一邊, 我需要綠寶石 2 個";
				mes "猴子尾巴 3 個, 觸鬚 10 個"; 
				mes "鳥嘴 10 個, 最後需要"; 
				mes "香蕉汁 36 瓶!";
				next;
				mes "[葛豪]";
				mes "找到之後一定要重新再來找我啊!";
				close;
			}
		}
		mes "[葛豪]";
		mes "噢噢... 你是弓箭手啊."; 
		mes "稍為再努力一點的話"; 
		mes "會得到不錯的名聲!";
		mes "請你繼續加油啊!"; 
		close;
	}
	mes "[?]";
	mes "像我一樣孤獨的人來說.";
	mes "還能對世界做出什麼貢獻呢?";
	close;

OnTouch_:
	mes "[???]";
	mes "咳嗚嗚嗚嗯....";
	close;
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 Roberto message text is based off RO npc.  Jason message
//=     text is custom from old version.
//=     All items are from official quests though.[kobra_k88]
//= 1.0a Now using functions found in "Global_Functions.txt"
//=     for class checks.[kobra_k88]
//= 1.2 Added Baby Class Support [Lupus]
//= 1.3 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 1.4 Fixed exploits [Lupus]
//= 1.4a Fixed some typos [IVBela]
//= 1.4b changed perm. variables to temp ones [Lupus]
//============================================================
