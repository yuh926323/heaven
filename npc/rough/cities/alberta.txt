//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================

//===== rAthena Script ======================================= 
//= Alberta Town
//===== By: ================================================== 
//= DZeroX
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Town-specific Alberta NPCs
//===== Additional Comments: ================================= 
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//= 1.1 Optimized, missing next, REMOVED .GATs! again [Lupus]
//= 1.2 Implemented progression fixes provided by $ephiroth. [L0ne_W0lf]
//= 1.2b More pregression clean up provided by Barron-Monster. [L0ne_W0lf]
//= 1.3 Removed npc "Tourist#al" as it's a quest NPC. [L0ne_W0lf]
//= 1.4 Removed "Fastidious Old Man" duplicate. [L0ne_W0lf]
//= 1.5 Updated to match AEGIS script. [Kisuka]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//============================================================

alberta,97,51,0	script	法比安	4_M_02,{
	mes "[法比安]";
	mes "嘿... 環遊世界的時候我可聽到了好多好玩的事兒.";
	next;
	mes "[法比安]";
	mes "我聽說過有些蘊含有魔物力量的卡片呢. 如果有人使用卡片的話, 那麼就可以使用魔物的力量.";
	next;
	mes "[法比安]";
	mes "我想啊, 這種讓你收集所有卡片的狂熱說不定是個什麼騙局呢. 我的意思是啊, 一張卡片怎麼能夠擁有魔物的力量呢?!";
	next;
	mes "[法比安]";
	mes "我可是很嚴肅的對待這件事情的啊...";
	close;
}

alberta,53,39,0	script	斯泰納	4W_SAILOR,{
	mes "[斯泰納]";
	mes "哦...!";
	mes "歡迎來到艾爾貝塔,";
	mes "年輕的冒險者!";
	next;
	mes "[斯泰納]";
	mes "如果我看起來很煩躁請不要責怪我. 我在這兒轉來轉去是想做個計劃的. 我聽說吉芬有一家店出售增加魔抗的裝備.";
	next;
	mes "[斯泰納]";
	mes "如果我把那些貨進來, 然後高價出售...";
	close;
}

alberta,20,183,0	script	乍得	1_M_03,{
	mes "[乍得]";
	mes "人們說傳說中的剛尼爾長矛帶有必中的攻擊效果. 不知道是不是真的...";
	next;
	mes "[乍得]";
	mes "人們也說嬰兒在出生前是由鶴送過來的, 也說女孩子喜歡戳那些蠢蛋男孩子, 還有人說聖誕老人真的存在的! 當然僅僅在姜餅城.";
	next;
	mes "[乍得]";
	mes "我不敢相信...";
	mes "即使這些東西之中";
	mes "只有一個是真的,";
	mes "我也不相信...";
	close;
}

alberta,131,139,2	script	醉醺醺的老頭	1_M_JOBGUIDER,{
	mes "[蒂亞戈]";
	mes "^666666*嗝*^000000";
	mes "你...你...";
	mes "看什麼呀你? 滾蛋!!";
	next;
	switch(select("一言不發.","讓這個傢伙一個人呆著去吧.")) {
		case 1:
			mes "[蒂亞戈]";
			mes "哈哈哈哈 ^666666*嗝*^000000... 你還蠻聰明的嘛. 雖然我現在看起來很沒用噶, 但是我以前是艦 '瑪麗之光'的一名水手";
			next;
			switch(select("沒聽說過.","真的嗎? 別開玩笑了!")) {
				case 1:
					mes "[蒂亞戈]";
					mes "沒聽說過嗎? 哪有一個人沒聽說過臭名昭著的 '瑪麗之光!' ^666666*嗝~*^000000";
					next;
					mes "[蒂亞戈]";
					mes "啊~ 那些舊時光啊. 如果... 如果沒有那場風暴...^666666*嗝*^000000";
					next;
					mes "[蒂亞戈]";
					mes "啊~ 船長, 好想念我們的船長啊... 沒有一個人可以接下來我們船長的劍的.";
					close;
				case 2:
					mes "[蒂亞戈]";
					mes "是啊! 沒有人敢和我們 '瑪麗之光!'作伴的! 也沒有人可以在劍術對決中打敗我們船長的!";
					next;
					mes "[蒂亞戈]";
					mes "船長啊~!!! ^666666*嗝~*^000000 他會這樣揮劍, 這樣, 這樣!!";
					next;
					mes "[蒂亞戈]";
					mes "船長所打的混蛋身旁所有的人都被火焰所圍繞啊";
					next;
					mes "[蒂亞戈]";
					mes "要不是那把大劍有神秘力量, 要不就是船長技術過人...!";
					next;
					mes "[蒂亞戈]";
					mes "呼呼~~ ^666666*嗚嗚* *嗚嗚...*^000000 上帝啊, 真的好想念大家啊! 唉,我現在很鬱悶, 你走吧.";
					close;
			}
		case 2:
			mes "[蒂亞戈]";
			mes "對了!";
			mes "走吧你~";
			close;
	}
}

alberta,58,80,2	script	沙啟爾	4W_M_03,{
	mes "[沙啟爾]";
	if (rand(2)) {
		mes "我們這些商人在買賣的時候有特別的討價還價的技巧. 這樣買別人的東西的時候可以賺得更多點.";
		next;
		mes "[沙啟爾]";
		mes "當然不是叫 '請便宜點吧!' 你要有魄力, 還要精通修辭!";
		next;
		mes "[沙啟爾]";
		mes "用這種神奇的能力我們可以得到省下 24 % 的錢. 但是這種技能是需要努力才學得會的!";
	} else {
		mes "作為我們商人來說.";
		mes "可以在路邊開設自己的露天商店.";
		mes "來做些生意.";
		next;
		mes "[沙啟爾]";
		mes "我們可以用打折技巧從鎮上的店裡面低價買進貨物, 然後裝進我們租來的手推車裡.";
		next;
		mes "[沙啟爾]";
		mes "之後呢, 我們就可以到處走喇, 然後把貨物賣出, 賺些錢!";
		next;
		mes "[沙啟爾]";
		mes "這樣做生意方便又安全. 雖然賣東西感覺很簡單, 但是可不要睡著...";
	}
	close;
}

alberta,62,156,2	script	宋雅	8_F,{
	mes "[宋雅]";
	switch(rand(3)){
	case 0:
		mes "這次我經過森林的時候看到了這個綠綠的, 在地上動來動去的莖.";
		next;
		mes "[宋雅]";
		mes "我就上前去看看這到底是什麼, 但是我到的時候啊, 這個莖居然從開始猛擊我的手了!!";
		next;
		mes "[宋雅]";
		mes "真心嚇人...所以我就跳了回去...然後我發現那個不是莖, 是一個小小的動物.";
		next;
		mes "[宋雅]";
		mes "真幸運我沒把它弄生氣. 如果激怒了他的話, 這個動物不管有多小都是很危險的.";
		close;
	case 1:
		mes "你知道那些在通往斐揚森林種住著的看上去懶洋洋的熊嗎?";
		next;
		mes "[宋雅]";
		mes "有一次覺得好玩, 我就朝一頭熊扔了一塊石頭, 然後突然他就衝向了我啊! 我好怕怕的當時! 然後就跑啊跑啊, 然後只聽見一聲巨響!!!";
		next;
		mes "[宋雅]";
		mes "他被一根很低的樹枝給撞暈倒了! 我自此發誓, 我以後再也不會隨便為了樂子激怒動物了!";
		close;
	case 2:
		mes "記得有一次我看見一堆狼和一些大笨熊在較量!";
		next;
		mes "[宋雅]";
		mes "那些狼啊, 比他們看上去的可要團結合作多了. 如果他們其中的一頭被攻擊的話, 那麼其他所有周圍的狼都會跑來幫助.";
		next;
		mes "[宋雅]";
		mes "如果旁邊還有其他這種類型的魔物的話, 你面前這個還是別打了吧, 小心他們聯合起來對付你啊!";
		close;
	}
}

alberta,93,174,2	script	艾瑪奶奶	8_F_GRANDMOTHER,{
	mes "[艾瑪奶奶]";
	mes "不久以前,";
	mes "一艘廢棄的船隻";
	mes "順著潮水";
	mes "飄進了艾爾貝塔的港口.";
	next;
	mes "[艾瑪奶奶]";
	mes "本想來看看有沒有生還者的, 所以一些鎮裡人就去船隻裡頭冒險看看. 結果他們都嚇壞了一樣的跑出來, 說一堆屍體在船隻裡面行走啊.";
	next;
	mes "[艾瑪奶奶]";
	mes "那艘船裡頭也有很多危險的水生生物. 鎮裡人即使是想走進去也走不進去啊.";
	next;
	mes "[艾瑪奶奶]";
	mes "我們都拿那個不吉利的船隻沒辦法, 那就讓那艘船這麼呆著吧. 現在有些遠征團已經開始進入船隻, 嘗試清除那些魔物.";
	next;
	mes "[艾瑪奶奶]";
	mes "所以像你這種年輕人, 又是新兵蛋子的, 去那邊冒冒險也不錯. 但是如果你比較弱小的話, 我建議你啊, 還是別冒著生命危險去的為妙.";
	close;
}

alberta,189,151,5	script	菲斯克	100,{
	mes "[菲斯克]";
	mes "嘿朋友,";
	mes "要去哪兒";
	mes "玩玩啊?";
	next;
	switch(select("沉船 -> 250 zeny.","依斯魯得海濱 -> 500 zeny.","沒事.")) {
		case 1:
			if (Zeny < 250) {
				mes "[菲斯克]";
				mes "嘿別騙我啊! 說了收 250 zeny的!";
				close;
			}
			set Zeny, Zeny - 250;
			warp "alb2trea",43,53;
			end;
		case 2:
			if (Zeny < 500) {
				mes "[菲斯克]";
				mes "沒有 500 Zeny 我才不會給你過去呢!";
				close;
			}
			set Zeny, Zeny - 500;
			if(checkre(0))
				warp "izlude",195,212;
			else
				warp "izlude",176,182;
			end;
		case 3:
			mes "[菲斯克]";
			mes "好吧...";
			mes "估計是航海菜鳥一個.";
			close;
	}
}

alb2trea,39,50,6	script	菲斯克#a2t	4W_SAILOR,{
	mes "[菲斯克]";
	mes "想要返回艾爾貝塔對吧?";
	next;
	if(select("是的.","變了主意了.")==1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	保羅	4_M_04,{
	mes "[保羅]";
	mes "真好的一天啊~";
	mes "想要加入我們沉船冒險團嗎?";
	mes "一起去那艘船上";
	mes "碰碰運氣";
	mes "看看能不能搞到點什麼?";
	next;
	mes "[保羅]";
	mes "哦! 我有件事情要先聲明一下, 如果你不夠強的話還是別去了.";
	next;
	mes "[保羅]";
	mes "想要加入嗎?";
	mes "申請費只要 200 zeny.";
	mes "只要你付了 200 zeny 之後.";
	mes "我們就是一夥的了.";
	mes "只要 200 Zeny.";
	next;
	switch(select("加入!","啊, 不用了.")) {
		case 1:
			if (Zeny < 200) {
				mes "[保羅]";
				mes "朋友, 你的錢不夠啊. 等你錢夠了來吧.";
				close;
			} else {
				set Zeny, Zeny - 200;
				warp "alb2trea",62,69;
				close;
			}
		case 2:
			mes "[保羅]";
			mes "好吧...";
			mes "如果你想通了就來找我吧.";
			mes "可別太久";
			mes "我們就要動身去冒險了.";
			close;
	}
}

alberta,190,173,4	script	菲利克斯	4_M_03,{
	set .@Weight,MaxWeight-Weight;
	mes "[菲利克斯]";
	if ((.@Weight) < 10000) {
		mes "等等啊!!";
		mes "你東西帶的太多了!";
		mes "你不能在拿東西了啊!!";
		mes "減輕點份量再來吧!";
		mes "我會在這裡等你.";
		close;
	}
	if (@event_zelopy == 0) {
		mes "你在這裡幹什麼?";
		mes "這艘船上不提供免費的東西的!!";
		next;
		mes "[菲利克斯]";
		mes "啊啊, 要不你給我 10 個傑勒比結晶, 我給你一個紅水, 好伐?";
		mes "如果你實在那麼娘兒們的話, 3 個傑勒比, 換一個胡蘿蔔.";
		next;
		mes "[菲利克斯]";
		mes "如果感興趣的話就把東西拿來吧.";
		set @event_zelopy,1;
		close;
	} else {
		mes "嗯, 你要換紅水 還是胡蘿蔔? 哪一個?";
		next;
		switch(select("紅水.","胡蘿蔔.")) {
		case 1:
			mes "[菲利克斯]";
			mes "那好...";
			mes "看看你帶來了幾個...";
			mes "有沒有達到要求...";
			next;
			mes "[菲利克斯]";
			if (countitem(909) < 10) {
				mes "你聾子啊? 說了 10 個傑勒比結晶換一個紅水的!";
				close;
			} else {
				set .@max,countitem(909)/10;
				mes "嗯, 不錯嘛...";
				mes "你要多少紅水?";
				mes "儘管開口.";
				next;
				switch(select("能換多少就換多少.","我來說要換多少吧.","唔, 我的結晶比你的東西要好多了, 才不換呢.")) {
				case 1:
					delitem 909,.@max*10;	// Jellopy
					getitem 501,.@max;	// Red_Potion
					break;
				case 2:
					mes "[菲利克斯]";
					mes "一次不超過 100 個所以別亂搞三七的好伐? 如果不要就輸入 '0'.";
					mes "你現在最多可以換 " + .@max + " 但是一定要記住, 一次最多 100 個?.";
					input .@amount;
					next;
					mes "[菲利克斯]";
					if (.@amount <= 0) {
						mes "多謝了, 要就來我地方吧.";
						close;
					}
					if (.@amount > 100) {
						mes "我不是說過了麼?一次最多 100 個, 你是想把我逼瘋是麼!";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "嗯啊, 你好像帶的東西不夠. 如果你要那麼多的話再去拿傑勒比結晶來我地方吧.";
						close;
					}
					delitem 909,.@amount*10;	// Jellopy
					getitem 501,.@amount;	// Red_Potion
					break;
				case 3:
					mes "[菲利克斯]";
					mes "沒問題,";
					mes "下次再見.";
					close;
				}
				mes "[菲利克斯]";
				mes "得了吧, 走吧你! 記得吃那些狗屎貨色的時候別噎著啊.";
				close;
			}
		case 2:
			mes "[菲利克斯]";
			mes "看看你有什麼多少...";
			next;
			mes "[菲利克斯]";
			if (countitem(909) < 3) {
				mes "死基佬, 滾粗去? 說了 3 個傑勒比結晶換一個胡蘿蔔的!";
				close;
			} else {
				set .@max,countitem(909)/3;
				mes "也不是特別娘兒們啊...";
				mes "要多少?";
				next;
				switch(select("能換多少換多少","數量我來定吧.","唔, 我的結晶比你的東西要好多了, 才不換呢.")) {
				case 1:
					delitem 909,.@max*3;	// Jellopy
					getitem 515,.@max;	// Carrot
					break;
				case 2:
					mes "[菲利克斯]";
					mes "一次不超過 100 個所以別亂搞三七的好伐? 如果不要就輸入 '0'.";
					input .@amount;
					next;
					mes "[菲利克斯]";
					if (.@amount == 0) {
						mes "那麼, 下回再見.";
						close;
					}
					if (.@amount > 100) {
						mes "死基佬, 一次最多 100 個, 我是不會和你這種人斷背的!";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "貌似你沒那麼多...你再去搞點來吧.";
						close;
					}
					delitem 909,.@amount*3;	// Jellopy
					getitem 515,.@amount;	// Carrot
					break;
				case 3:
					mes "[菲利克斯]";
					mes "哼, 小樣, 你的菊花早晚是我的~";
					close;
				}
				mes "[菲利克斯]";
				mes "走吧走吧~! 別自己打腫臉撐胖子.";
				close;
			}
		}
	}
}
