//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================

//===== rAthena Script =======================================
//= Buying Shop Scripts
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.1b
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Buying Shop licenses.
//===== Additional Comments: =================================
//= 1.0 First version. [Kisuka]
//= 1.1 Added Catalog Magician. [Euphy]
//= 1.1a Moved Catalog Magician to Renewal path. [Euphy]
//= 1.1b Added missing weight checks. [Euphy]
//============================================================ 

// Black Marketeer (Buy Licenses - Non-Merchant Classes)
//============================================================ 
que_job01,68,84,1	script	黑市商人#Buying	4_M_SITDOWN,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2400) {
		mes "[賈斯先生]";
		mes "You're too greedy, even compared to someone like me!";
		mes "Why don't you go lighten your bag first?";
		close;
	}
	if (getskilllv("ALL_BUYING_STORE") == 1) {
		mes "[賈斯先生]";
		mes "嗯? 是和修先生有關係的人阿!";
		mes "我跟你沒什麼好說的.";
		close;
	}
	mes "[賈斯先生]";
	mes "居然被你找到這裡了.";
	mes "你想要什麼呢?";
	next;
	switch(select("採購商店執照:你是誰?:不需要!")) {
	case 1:
		mes "[賈斯先生]";
		mes "好的, 好的!";
		mes "我來幫你做.";
		mes "保證做得跟真的一模一樣!";
		next;
		mes "[賈斯先生]";
		mes "我的執照較好, 因為你不需要去學習任何使用技能.";
		mes "你想要多少?";
		next;
		mes "[賈斯先生]";
		mes "一次不能製作很多, 每次最多10張.";
		mes "手續費是每張 500 金幣.";
		next;
		while(1) {
			input .@input;
			mes "[賈斯先生]";
			if (.@input == 0) {
				mes "不需要嗎?";
				close;
			} else if (.@input > 10) {
				mes "你知道我每次最多只能製作10張.";
				next;
			} else {
				mes "手續費一共是 "+(.@input*500)+" zeny.";
				if (Zeny < (.@input*500)) {
					mes "你的錢好像不夠阿.";
					next;
						mes "[賈斯先生]";
						mes "雖然我沒資格講商業道德, 但世界上應該遵循等價交換原則.";
						mes "如果你想把東西拿走就必須支付相等的代價.";
				} else {
						mes "呵呵呵... 呵呵呵!";
						mes "修先生, 怎麼樣?我要成為地下世界的黑手!";
						mes "*呵呵呵*";
					getitem 12548,.@input; //Shabby_Purchase_Street
					set Zeny, Zeny - (.@input*500);
				}
				close;
			}
		}
	case 2:
		mes "[賈斯先生]";
		mes "我離開故鄉已經很久了,";
		mes "那種問題對我來說沒有任何意義, 對我而言, 剩下的只有憎恨....";
		next;
		mes "[賈斯先生]";
		mes "...";
		mes "正確的商道是什麼呢...對於已經墮落的我來說, 剩下的還有什麼呢?";
		mes "要是能讓修先生那各傢伙吃點苦頭...";
		next;
		mes "[賈斯先生]";
		mes "哎呀, 我喝醉了... (*嗝*)";
		mes "把我說的忘了吧!";
		mes "請一定要記住我說的話.";
		close;
	case 3:
		mes "[賈斯先生]";
		mes "不要就不要, 幹嘛那麼大聲?";
		mes "現在的年輕人.....";
		close;
	}
}

// Purchasing Team (Learn Skill - Merchant Classes)
//============================================================ 
alberta_in,58,52,4	script	採購部修先生#Buying	1_M_ORIENT01,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2400) {
		mes "- You cannot converse or perform the quest because you are carrying too many items. -";
		close;
	}
	mes "[修先生]";
	if (BaseClass == Job_Merchant && getskilllv("MC_VENDING") >= 1) {
		if (getskilllv("ALL_BUYING_STORE") == 1) {
			mes "我是採購部的修先生.";
			mes "你需要什麼東西呢?";
			next;
			if(select("購買採購商店執照:不需要") == 2) {
				mes "[修先生]";
				mes "如果需要開設採購商店所需的執照的話.";
				mes "請隨時來這裡申請, 歡迎下次光臨~";
				close;
			}
			mes "[修先生]";
			mes "採購商店執照每張售價 200 金幣, 一次最多可以購買50張.";
			mes "你想購買多少張1?";
			next;
			while(1) {
				input .@input;
				mes "[修先生]";
				if (.@input == 0) {
					mes "您已取消交易.";
					mes "祝您有美好的一天.";
					close;
				} else if (.@input > 50) {
					mes "請輸入購買張數. 至少50張起.";
					next;
				} else {
					mes "花費 "+(.@input*200)+" zeny "+.@input+" 執照.";
					if (Zeny < (.@input*200)) {
						mes "你沒有足夠的錢阿.";
					} else {
						mes "謝謝您的惠顧.";
						getitem 6377,.@input; //Buy_Stall_Permit
						set Zeny, Zeny - (.@input*200);
					}
					close;
				}
			}
		} else {
			mes "我是艾爾貝塔商人公會採購部的修先生.";
			mes "你....";
			next;
			mes "[修先生]";
			mes "你知道嗎? 這次商人公會也允許一般人購買.";
			next;
			select("沒有那個也能買東西啊...");
			mes "[修先生]";
			mes "是的,";
			mes "但是你想要購買大量商品的時候,";
			mes "是不是覺得很辛苦呢?";
			mes "還要去找把東西帶在身上或有意出售的人才行.";
			next;
			mes "[修先生]";
			mes "物品的大量流通, 對於商人來說是非常重要的事情,";
			mes "因此在採購部工作了十年的我, 這次提出了劃時代的提案.";
			next;
			mes "[修先生]";
			mes "就是個人";
			mes "也可以開設採購商店!";
			mes "你只要";
			mes "看了這份提案就知道了...";
			next;
			mes "[修先生]";
			mes "...";
			mes "...(修先生自顧自地說著.)";
			next;
			select("夠了, 說說結論?");
			mes "[修先生]";
			mes "嗯, 好吧. 簡單地概括一下就是,";
			mes "允許部份商品透過商店大量出售.";
			next;
			mes "[修先生]";
			mes "想要開設採購商店, 須要有商人公會的執照.";
			next;
			mes "[修先生]";
			mes "每開設一次, 都需使用執照. 出售執照帶來的收入, 可以未商人公會打下堅固的基礎.";
			next;
			mes "[修先生]";
			mes "這部分是我的提案核心!";
			mes "會長非常高興, 很快就通過了我的提案!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "只說結論就行!";
			next;
			mes "[修先生]";
			mes "啊, 你的性子可真急.";
			mes "所以我們允許擁有開設銷售商店授權的人, 開採購商店.";
			next;
			mes "[修先生]";
			mes "當然, 需要 10,000 金幣的註冊費.";
			mes "每次開設商店還需要 ^4A4AFF採購商店執照^000000 .";
			next;
			mes "[修先生]";
			mes "怎樣? 你想註冊嗎?";
			mes "註冊之後, 我就會告訴你開設採購商店的方法.";
			next;
			if(select("學習開設採購商店:不學習") == 2) {
				mes "[修先生]";
				mes "唉, 真讓人遺憾!";
				mes "這可以讓你方便很多的, 如果感興趣的話,";
				mes "可以再來找我, 我永遠都歡迎你過來!";
				close;
			}
			mes "[修先生]";
			if (Zeny < 10000) {
				mes "登記費為 10,000 金幣.";
				mes "費用請準備好.";
				close;
			} else {
				mes "明智的決定.";
				mes "好的,請先到這邊來註冊, 在這裡簽名....";
				next;
				input .@name$;
				mes "[修先生]";
				mes .@name$+"....";
				mes "字寫得很不錯嘛!";
				mes "現在你可以開設採購商店了.";
				set Zeny, Zeny - 10000;
				getitem 6377,5; //Buy_Stall_Permit
				skill "ALL_BUYING_STORE",1,3;
				next;
				mes "[修先生]";
				mes "可以大量購買的物品是除裝備和部分藥水/個人製作的物品^8C2121以外^000000的普通物品, 但根據應用方法的不同, 我想應該會對個人很有幫助.";
				next;
				mes "[修先生]";
				mes "你至少需要庫存一個收購物品, 因為要透過商店展讓大家知道要收購的是什麼物品.";
				next;
				mes "[修先生]";
				mes "購買商店技能應該登錄在您的技能視窗了. 如果沒看見您可能需切換技能視窗顯示模式在檢查確認一下.";
				next;
				mes "[修先生]";
				mes "我已經給你5張試用的採購商店執照.";
				mes "體驗採購樂趣!";
				close;
			}
		}
	}
	mes "我是艾爾貝塔商人公會採購部的修先生.";
	mes "我喜歡聊天但是我現在很忙碌.";
	next;
	mes "[修先生]";
	mes "我的時間完全奉獻給廣大的商業顧客.";
	close;
}
