//===== rAthenaCN 漢化聲明 ===================================
//= 您可以隨意轉載和使用本漢化腳本。若您使用、借鑒或複製了
//= 此腳本中的漢化成果，那麼請在您的腳本中加上並保留這七行
//= 漢化聲明，由衷感謝。
//= 若您對這個腳本有更好的翻譯建議，或發現此腳本的漢化錯誤，
//= 請前往 http://rathena.cn 上報您發現的問題，謝謝。
//============================================================

//===== rAthena Script ======================================= 
//= Malangdo Town
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Malangdo Town Script
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Added entrance scripts.
//= 1.2 Moved "Odgnalam" coordinates.
//= 1.3 Fixed dialogues.
//= 1.4 Dialogue edits, source iRO.
//= 1.5 Added Izlude duplicates.
//============================================================

// Malangdo Transportation :: malangdo_go
//============================================================
-	script	Dolangmal	-1,{
	if (checkquest(5091) == -1) {
		mes "[貓綿綿]";
		mes "嘿~! 人類你好呀~ 我是貓綿綿. 哇卡卡~";
		next;
		mes "[貓綿綿]";
		mes "我是被派來接你去貓咪的樂園^A2314B綿綿島^000000的.";
		next;
		mes "[貓綿綿]";
		switch(atoi(strnpcinfo(2))) {
			case 1: mes "怎麼樣? 雖然人聲鼎沸的普隆德拉也不錯啦, 但你有考慮過去我們貓咪的樂園^A2314B綿綿島^000000一探究竟嗎?"; break;
			case 2: mes "與其來充斥著死腦筋魔法師的吉芬, 不如去美麗的貓咪樂園^A2314B綿綿島^000000吧! 怎麼樣?"; break;
			case 3: mes "雖然斐楊的空氣很不錯, 但是美麗的貓咪樂園^A2314B綿綿島^000000的空氣比這裡還好噢~ 要不要去?"; break;
			case 4: mes "比起充滿滴答滴答嘈雜鐘聲的艾爾帕蘭, 美麗又安靜的貓咪樂園^A2314B綿綿島^000000更好吧! 要不要去呀?"; break;
			case 5: mes "比起又熱又颳風沙的夢羅克, 吹海風的貓咪樂園^A2314B綿綿島^000000更好啦! 要不要去呀?"; break;
			case 6: mes "怎麼樣? 有興趣離開這讓眼睛和腦袋都疼的, 到處都是書的朱諾, 到我們貓咪的樂園^A2314B綿綿島^000000嗎?"; break;
			case 7: mes "怎麼樣? 有興趣離開靜到嚇人的拉赫, 到我們熱鬧的貓咪樂園^A2314B綿綿島^000000嗎?"; break;
			case 8: mes "怎麼樣? 想離開滿街充斥著黑社會的裡希塔樂鎮, 到我們安全的貓咪樂園^A2314B綿綿島^000000嗎?"; break;
			case 9: mes "怎麼樣? 比起這到處都是小草的味道, 有著大海味道的貓咪樂園^A2314B綿綿島^000000更好啦! 要不要去呀?"; break;
		}
		next;
		if(select("不要:往^A2314B綿綿島^000000出發咯~ Go~ Go~") == 1) {
			mes "[貓綿綿]";
			mes "我擦!? 你說你不想去^A2314B綿綿島^000000? 嗟!";
			next;
			mes "[貓綿綿]";
			mes "你不去的話幹嘛要跟我搭話... 幹嘛煩我...不過...";
			next;
			mes "[貓綿綿]";
			mes "^9E1837湯瑪斯司令^000000要我來綁架人類.. 呃.. 應該是邀請人類吧？";
			next;
			mes "[貓綿綿]";
			mes "這次放你一馬! 想去的話隨時告訴我喔~";
			close;
		}
		mes "[貓綿綿]";
		mes "很好! 我欣賞你的熱情奔放!";
		next;
		mes "[貓綿綿]";
		mes "在你前往^A2314B綿綿島^000000之前, 有幾點注意事項要提醒你.";
		next;
		mes "[貓綿綿]";
		mes "第一點!! 當你抵達^A2314B綿綿島^000000後, 必須先向^9E1837湯瑪斯司令^000000報到..";
		next;
		mes "[貓綿綿]";
		mes "不然的話, 你可能會在昏暗的路上, 遭到可怕粗暴的貓咪大哥們的攻擊.";
		next;
		mes "[貓綿綿]";
		mes "第二點!! 雖然這裡開放給人類造訪, 但是^A2314B綿綿島^000000100%是我們貓咪的地盤!";
		next;
		mes "[貓綿綿]";
		mes "要把我們當作一個人...";
		mes "喔... 不... 我們是貓嘛~";
		mes "要把我們當作一隻貓來溫柔對待!";
		next;
		mes "[貓綿綿]";
		mes "然後是...";
		next;
		mes "明明說只有幾點注意事項的貓綿綿, 滔滔不絕地說了100多點的注意事項.";
		next;
		mes "[貓綿綿]";
		mes "很好! 以上就是注意事項!! 都是些很容易遵守的事... 怎麼樣? 你有辦法遵守嗎?";
		next;
		switch(select("我可以遵守:沒辦法遵守")) {
		case 1:
			mes "[貓綿綿]";
			mes "很好! 說話算話哈.";
			mes "^9E1837湯瑪斯司令^000000在^A2314B綿綿島中央的船^000000里.";
			next;
			mes "[貓綿綿]";
			mes "別忘記要去報到喔~";
			next;
			mes "[貓綿綿]";
			mes "好啦, 我不囉嗦了.";
			mes "讓我們一起前往貓咪的樂園^A2314B綿綿島^000000吧~";
			setquest 5091;
			completequest 5091;
			warp "malangdo",217,85;
			close;
		case 2:
			mes "[貓綿綿]";
			mes "你說什麼!? 連這麼簡單的規則也沒辦法遵守? 嗟!";
			next;
			mes "[貓綿綿]";
			mes "如果你沒辦法遵守就早說嘛~ 還讓我說得那麼累...";
			next;
			mes "[貓綿綿]";
			mes "^9E1837湯瑪斯司令^000000要我來綁架人類.. 呃.. 應該是邀請人類吧？";
			next;
			mes "[貓綿綿]";
			mes "這次放你一馬! 想去的話隨時告訴我喔~";
			close;
		}
	}
	mes "[貓綿綿]";
	mes "嗯? 你不是上次...?";
	mes "好吧. 你去^A2314B綿綿島^000000回來啦?";
	next;
	switch(select("再送我去一次^A2314B綿綿島^000000吧:嗯, 我回來拉!")) {
	case 1:
		mes "[貓綿綿]";
		mes "我也很想送你去呢! 但是規則不允許嘛~ 我光送第一次去^A2314B綿綿島^000000的人就夠累的!";
		next;
		mes "[貓綿綿]";
		mes "而且你根本沒有聽完整我說的規則嘛... 第53條規則...";
		next;
		mes "[貓綿綿]";
		mes "我再特別告訴你一次第53條規則吧! 誰教我是心胸很寬大的貓咪呢~";
		next;
		mes "[貓綿綿]";
		mes "如果你想再次去^A2314B綿綿島^000000的話, 就得去依斯魯得島或是艾爾貝塔搭船.";
		next;
		mes "[貓綿綿]";
		mes "搭上船後, 一直走~";
		mes "就會抵達我們貓咪的樂園^A2314B綿綿島^000000啦.";
		next;
		mes "[貓綿綿]";
		mes "很簡單吧? 那就祝你一路順風啦~ 喵";
		close;
	case 2:
		mes "[貓綿綿]";
		mes "怎麼樣? 不錯吧? 我就知道! 哇卡卡~";
		next;
		mes "[貓綿綿]";
		mes "對了! 我怕你不知道...";
		next;
		mes "[貓綿綿]";
		mes "如果你想再次去^A2314B綿綿島^000000的話, 得去依斯魯得島或者艾爾貝塔搭船才行.";
		next;
		mes "[貓綿綿]";
		mes "嘛~ 你這麼聰明一定知道吧!";
		mes "那我們下次再見啦~";
		close;
	}
}
prontera,114,77,6	duplicate(Dolangmal)	貓綿綿#1	553
geffen,109,61,6	duplicate(Dolangmal)	貓綿綿#2	553
payon,200,106,4	duplicate(Dolangmal)	貓綿綿#3	553
aldebaran,170,104,4	duplicate(Dolangmal)	貓綿綿#4	553
morocc,168,275,4	duplicate(Dolangmal)	貓綿綿#5	553
yuno,165,122,4	duplicate(Dolangmal)	貓綿綿#6	553
rachel,121,126,4	duplicate(Dolangmal)	貓綿綿#7	553
lighthalzen,254,83,5	duplicate(Dolangmal)	貓綿綿#8	553
mora,57,150,4	duplicate(Dolangmal)	貓綿綿#9	553

-	script	::Odgnalam	-1,{
	mes "貓咪曬著"+((strnpcinfo(2) == "albe")?"艾爾貝塔":"依斯魯德島")+"的溫暖太陽, 睡得很香...";
	next;
	if(select("讓貓咪繼續睡, 不打擾它:和貓咪說話") == 1) close;
	mes "[貓奧克]";
	mes "喵嗚? 嗯?";
	next;
	if (ep13_yong1 == 0) {
		set .@price,1000;
		set .@s1$,"哼!! 原來是煩人的人類! 給我^0000FF1000金幣^000000我就送你去貓咪樂園^A2314B綿綿島^000000!";
		set .@s2$,"錢呢? 錢在哪裡? 沒有錢你還想去哪呀? 人類臉皮真厚!";
	} else if (ep13_yong1 < 60) {
		set .@price,999;
		set .@s1$,"你和貓咪商隊還蠻有緣的嘛! 看在我們這麼有緣的份上, 給你打個折, 給我^0000FF999金幣^000000我就送你去^A2314B綿綿島^000000!";
		set .@s2$,"錢在哪裡? 你怎麼一點榮譽感都沒有?";
	} else if (ep13_yong1 > 59 && ep13_yong1 < 80) {
		set .@price,500;
		set .@s1$,"你曾經給過我們貓咪一些幫助. 這次你只需要給我一半的費用(^0000FF即500金幣^000000), 我就送你去貓咪樂園^A2314B綿綿島^000000!";
		set .@s2$,"簡直不敢相信, 你居然沒有錢支付已經打五折的傳送費?";
	} else {
		mes "[貓奧克]";
		mes "你是貓咪商隊的英雄, 你的事跡已經成為我們的傳說.";
		next;
		mes "[貓奧克]";
		mes "因為你是貓咪商隊的英雄, 所以我會免費送你去貓咪樂園^A2314B綿綿島^000000.";
		next;
		if(select("出發前往綿綿島!!:不想去了...") == 1) {
			mes "[貓奧克]";
			mes "祝你有個舒適的旅程... 好啦, 我們出發~";
			warp "malangdo",217,85;
		}
		close;
	}
	mes "[貓奧克]";
	mes .@s1$;
	next;
	if(select("出發前往綿綿島!!:不想去了...") == 2) close;
	if (Zeny < .@price) {
		mes "[貓奧克]";
		mes .@s2$;
		close;
	}
	set Zeny, Zeny - .@price;
	mes "[貓奧克]";
	mes "現在馬上就送你過去! 出發~";
	warp "malangdo",217,85;
	close;
}
alberta,200,151,4	duplicate(Odgnalam)	貓奧克#albe	554
izlude,182,218,4	duplicate(Odgnalam)	貓奧克#iz	554	// Old coordinates: (182,192)
izlude_a,182,218,4	duplicate(Odgnalam)	貓奧克#iz_a	554
izlude_b,182,218,4	duplicate(Odgnalam)	貓奧克#iz_b	554
izlude_c,182,218,4	duplicate(Odgnalam)	貓奧克#iz_c	554
izlude_d,182,218,4	duplicate(Odgnalam)	貓奧克#iz_d	554

malangdo,219,86,4	script	貓豆豆#malang	545,{
	mes "[貓豆豆]";
	mes "在綿綿島玩得還開心嗎?";
	mes "我們貓咪商隊有提供來往於依斯魯得島和艾爾貝塔的便船.";
	next;
	if (ep13_yong1 == 0) {
		set .@price,1000;
		set .@s1$, "1000 金幣";
		set .@s2$, "祝你今天在綿綿島上過得愉快~";
		set .@no_money$, "搭船費用很難做到全免, 請諒解~";
		set .@yes_money$, "祝你旅途愉快~";
		mes "[貓豆豆]";
		mes "想搭船的話需要 1000 金幣, 你的目的地是哪裡?";
		next;
	} else if (ep13_yong1 < 60) {
		set .@price,999;
		set .@s1$,"999 金幣";
		set .@s2$, "祝你在綿綿島上事事順利...";
		set .@no_money$, "即使你和我們貓咪商隊有緣, 搭船費用很難做到全免, 請諒解~";
		set .@yes_money$, "謝謝, 祝你旅途愉快.";
		mes "[貓豆豆]";
		mes "看在你和貓咪商隊的緣分上, 特別給你個打折~ 船費只要 999 金幣, 你想去哪裡?";
		next;
	} else if (ep13_yong1 > 59 && ep13_yong1 < 80) {
		set .@price,500;
		set .@s1$,"500 金幣";
		set .@s2$, "祝你今天在綿綿島上超級好運, 然後還能中大獎.";
		set .@no_money$, "我的天. 你連500金幣都沒有? 搭船費用很難做到全免, 請諒解~";
		set .@yes_money$, "祝你旅途愉快, 歡迎下次再來.";
		mes "[貓豆豆]";
		mes "哇噢~ 你為我們做了很多奉獻... 好吧!! 給你特別優惠價 500 金幣. 你想去哪裡?";
		next;
	} else {
		set .@price,0;
		set .@s1$,"英雄免費!!";
		set .@s2$, "沒有人會反對貓咪商隊的英雄停留在綿綿島, 絕對沒有...";
		mes "[貓豆豆]";
		mes "啊... 你是貓咪商隊的英雄, 你是我們的傳奇!";
		next;
		mes "[貓豆豆]";
		mes "我們不能收取貓咪商隊英雄的費用. 尊敬的您想去哪裡?";
		next;
	}
	set .@i, select("- 往依斯魯得 --- "+.@s1$+":- 往艾爾貝塔 --- "+.@s1$+":- 留在綿綿島");
	mes "[貓豆豆]";
	if (.@i == 3) {
		mes .@s2$;
		close;
	}
	if (ep13_yong1 > 80) {
		mes "英雄, 祝您旅途愉快.";
		if (.@i == 1)
			warp "izlude",195,212; // Old coordinates: (194,180)
		else
			warp "alberta",192,150;
		close;
	}
	if (Zeny < .@price) {
		mes .@no_money$;
		close;
	}
	set Zeny, Zeny - .@price;
	mes .@yes_money$;
	if (.@i == 1)
		warp "izlude",195,212;
	else
		warp "alberta",192,150;
	close;
}

// Generic Malangdo NPCs :: malang_tre
//============================================================
malangdo,147,117,3	script	Innkeeper#malang	554,{
	mes "You see a lazy cat standing on a chair.";
	mes "You guess that this is the Innkeeper.";
	next;
	switch(select("Umm... Can I rest here?:Psst... Can I save here?:Leave.")) {
	case 1:
		mes "[Innkeeper]";
		mes "Eh?";
		mes "Uh, sure you can use this hammock if you give me ^FF00005 Malangdo Cans^000000.";
		next;
		switch(select("Here you go.:Never mind.")) {
		case 1:
			if (countitem(12636) > 4) {
				delitem 12636,5; //Malang_Sp_Can
				percentheal 100,100;
				specialeffect2 EF_HEALSP;
				mes "[Innkeeper]";
				mes "Now relax.";
				emotion e_kis;
				next;
				warp "malangdo",140,121;
				close;
			}
			mes "[Innkeeper]";
			mes "This ain't no charity.";
			close;
		case 2:
			mes "[Innkeeper]";
			mes "Let me know if you want to relax.";
			close;
		}
	case 2:
		mes "[Innkeeper]";
		mes "Location saved. We should keep company, haha~";
		savepoint "malangdo",142,118,1,1;
		close;
	case 3:
		mes "[Innkeeper]";
		mes "Sometimes you just have to relax.";
		next;
		mes "- Wow, this is the laziest cat ever. -";
		close;
	}
	close;
}

malangdo,151,120,3	script	Minstrel#mal	479,{
	mes "[Minstrel]";
	mes "Boy, that lazy cat makes me want to just nap all day long.";
	next;
	mes "[Minstrel]";
	mes "He makes life look so simple.";
	close;
}

malangdo,149,120,5	script	Wanderer#mal	485,5,1,{
	mes "[Wanderer]";
	mes "The cats living in here are so energetic.";
	mes "Is it weird that they make me want to dance all day?";
	mes "La di dah di dah~!";
	close;
OnTouch:
	switch(rand(5)) {
	case 0:
		soundeffect "ring_of_nibelungen.wav",0;
		break;
	case 1:
		soundeffect "dont_forget_me_not.wav",0;
		break;
	case 2:
		soundeffect "humming.wav",0;
		break;
	case 3:
		soundeffect "assassin_of_sunset.wav",0;
		break;
	case 4: 
		emotion e_ho;
		emotion e_ho,0,"Wandering Minstrel#mal";
		break;
	}
	end;
}

malangdo,216,168,3	script	Cat Trainer#mal1	558,0,8,{
	emotion e_awsm;
	mes "[Cat Trainer]";
	mes "Ooo~ their soft puffy tails and soft fur~";
	mes "I can't stand it, this is heaven~~!!";
	next;
	emotion e_gg;
	mes "[Cat Trainer]";
	mes "My hidden card, ^FF0000Silvervine Fruit^000000, can make all cats in Malangdo my slaves~ haha!";
	next;
	select("^FF0000Silvervine Fruit^000000?");
	mes "[Cat Trainer]";
	mes "It is such a rare item!";
	mes "I can barely get it.";
	mes "But these cudly cats can't resist it~";
	next;
	select("Where can I get ^FF0000Silvervine Fruit^000000?");
	mes "[Cat Trainer]";
	mes "Why would I tell you when I can barely get it?";
	mes "I wouldn't even tell you for all the zeny in the world~!";
	next;
	emotion e_gg;
	mes "[Cat Trainer]";
	mes "All the Malangdo cats are mine~!";
	mes "Muahahahaha~";
	mes "With enough ^FF0000Silvervine Fruit^000000 I can be a queen in Malangdo~ ohhhh!!!";
	close;
OnTouch:
	emotion e_flash;
	end;
}

malangdo,164,203,3	script	Cat Trainer#mal2	853,{
	emotion e_awsm;
	mes "[Cat Trainer]";
	mes "Ooo~ their soft puffy tails and soft fur~";
	mes "I can't stand it, this is heaven~~!!";
	next;
	emotion e_gg;
	mes "[Cat Trainer]";
	mes "This is my little secret called";
	mes "^FF0000Silvervine Fruit^000000. I can use";
	mes "it to make the Malangdo cats my";
	mes "slaves~ muahahahaha~!";
	next;
	select("^FF0000Silvervine Fruit^000000?");
	mes "[Cat Trainer]";
	mes "Hmm, I guess it's no wonder you don't know.";
	mes "These Malangdo cats can't resist it.";
	mes "But it's too rare of an item to get so you shouldn't bother trying to find it.";
	next;
	select("Where can I get ^FF0000Silvervine Fruit^000000?");
	mes "[Cat Trainer]";
	mes "Actually, it's simple. You just...";
	next;
	mes "[Cat Trainer]";
	mes "Hey, you almost made me tell my secret.";
	next;
	emotion e_gg;
	mes "[Cat Trainer]";
	mes "One day these cats will be mine.";
	mes "I can be a king in Malangdo~ ohhh~!";
	close;
}
