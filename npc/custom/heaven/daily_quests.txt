/*
作者: 阿神ˇ
作業環境: eA、SeAr
說明:        會自動搜尋擊殺魔物
魔物的等級範圍會與腳色等級相差5(預設:5)
警告: 請確認您的 getmonsterinfo 的type是否支援到22
*/
function	script	Reward	{                 //獎勵
	switch(getarg(0)){
		case 1: return getmonsterinfo(MustKill,3) * KillAmount * 2;                //Base  可帶入公式
		case 2: return getmonsterinfo(MustKill,4) * KillAmount * 2;                //Job 可帶入公式
		case 3: return (getmonsterinfo(MustKill,1) * KillAmount) / 10 <= 0 ? 50 : (getmonsterinfo(MustKill,1)*KillAmount)/10;                //shop ?可帶入公式
		case 4: if(getmonsterinfo(MustKill,22)>0) return getmonsterinfo(MustKill,22); else return 0;
		default: return -1;
	}
}
function	script	Searching	{
	while(set(.@v,.@v+1)){
		if(.@v%10==0) sleep2 1;
		set @t,0;
		set .@i,rand(1001,2085);
		if(!strmobinfo(3,.@i)) continue;
		if(getmonsterinfo(.@i,22)>0) continue;
		for(set .@q,0; .@q<getarraysize($@exception$); set .@q,.@q+1)
			if(compare(getmonsterinfo(.@i,0),$@exception$[.@q])==1) set @t,1;
		if(@t) continue;
		if(getmonsterinfo(.@i,1)<BaseLevel+getarg(0) && getmonsterinfo(.@i,1)>BaseLevel-getarg(0) && getmonsterinfo(.@i,3)>0 && getmonsterinfo(.@i,4)>0 ) break;
	}
	return .@i;
}
function	script	CheckAmount	{
	if(getarg(0)>1 && getarg(0)<15) return rand(5,30);
	else if(getarg(0)<30) return rand(5,40);
	else if(getarg(0)>=30 && getarg(0) <50) return rand(40,50);
	else if(getarg(0)>=50 && getarg(0) <70) return rand(20,30);
	else if(getarg(0)>=70 && getarg(0) <90) return rand(10,25);
	else return rand(1,5);
}
fayon, 84, 123,5	script	魔物獵人	753,{
	mes .npcname$;
	mes "我將會以你現在的等級";
	mes "來分配工作給你，完成後會有報酬";
	mes "^ff0000※Lv.90UP 不適合此任務^000000";
	next;
	if( !getgmlevel() && gettimetick(2)-#OT<86400) {
		mes .npcname$;
		mes "- 延遲時間剩餘: ^ff0000"+(86400-(gettimetick(2)-#OT))+"^000000 秒 -";
		close;
	}
	if(MustKill){
		if(select(KillPoint>=KillAmount?"提交任務(^888888任務已完成^000000)":"查看任務內容","放棄^FF1493'"+getmonsterinfo(MustKill,0)+"'^000000任務")==2) {
			set MustKill,0;
			set KillAmount,0;
			set KillPoint,0;
			if($CheckMode) set #OT,gettimetick(2);
			mes .npcname$;
			mes "^FF1493本次任務被取消^000000";
		}
		else if(KillPoint>=KillAmount){
			mes .npcname$;
			mes "恭喜你完成本次的魔物獵殺任務";
			mes "本次獲得的報酬";
			mes "Base經驗值 ^FF1493"+callfunc("Reward",1)+"^000000";
			mes "Job經驗值 ^FF1493"+callfunc("Reward",2)+"^000000";
			mes "Cash點數 ^FF1493"+callfunc("Reward",3)+"^000000";
			set BaseExp,BaseExp+callfunc("Reward",1);
			set JobExp,JobExp+callfunc("Reward",2);
			set #CASHPOINTS,#CASHPOINTS+callfunc("Reward",3);
			set MustKill,0;
			set KillAmount,0;
			set KillPoint,0;
			set #OT,gettimetick(2);
		}
		else {
			mes .npcname$;
			mes "本次的魔物獵殺任務內容";
			mes "^0088FF魔物名稱^000000: ^FF1493"+getmonsterinfo(MustKill,0)+"^000000";
			mes "^0088FF魔物等級^000000: ^FF1493"+getmonsterinfo(MustKill,1)+"";
			mes "^0088FF數量^000000:[ ^009900"+KillPoint+"^000000/^FF6600"+KillAmount+"^000000 ]";
			next;
			mes "本次的魔物獵殺任務酬勞";
			mes "^0088FFBase經驗值^000000: ^FF1493"+callfunc("Reward",1)+"^000000";
			mes "^0088FFJob經驗值^000000: ^FF1493"+callfunc("Reward",2)+"^000000";
			mes "^0088FFCash點數^000000: ^FF1493"+callfunc("Reward",3)+"^000000";
		}
		close;
	}
	else{
		set MustKill,callfunc("Searching",5);
		set KillAmount,callfunc("CheckAmount",getmonsterinfo(MustKill,1));
		mes .npcname$;
		mes "本次的魔物獵殺任務內容";
		mes "^0088FF魔物名稱^000000: ^FF1493"+getmonsterinfo(MustKill,0)+"^000000";
		mes "^0088FF魔物等級^000000: ^FF1493"+getmonsterinfo(MustKill,1)+"";
		mes "^0088FF數量^000000:[ ^009900"+KillPoint+"^000000/^FF6600"+KillAmount+"^000000 ]";
		next;
		mes "本次的魔物獵殺任務酬勞";
		mes "^0088FFBase經驗值^000000: ^FF1493"+callfunc("Reward",1)+"^000000";
		mes "^0088FFJob經驗值^000000: ^FF1493"+callfunc("Reward",2)+"^000000";
		mes "^0088FFCash點數^000000: ^FF1493"+callfunc("Reward",3)+"^000000";
		close;
	}
OnNPCKillEvent:
	if(MustKill && KillPoint<KillAmount && killedrid == MustKill){
		set KillPoint,KillPoint+1;
		dispbottom ""+getmonsterinfo(MustKill,0)+": [ "+KillPoint+"/"+KillAmount+" ]";
	}
end;
OnInit:
	set .npcname$,"["+strnpcinfo(0)+"]";
	setarray $@exception$,"真晶","炸彈","聖誕","肉粽","寶箱","箱子","寶物箱子","旗幟","守護石","防禦線","水晶","華麗金屬","開關"; // 避免搜尋到的魔物 (例: 華麗金屬)
	end;
}