/*
作者: 阿神ˇ
修改: Dragon
作業環境: eA、SeAr、rA
說明:        會自動搜尋擊殺魔物
魔物的等級範圍會與腳色等級相差5(預設:5)
*/

function	script	Reward	{
    set .type, getarg(0);
    switch(.type) {
        case 1:
            return getmonsterinfo(MustKill, MOB_BASEEXP) * KillAmount * $@BASE_RATE;
        case 2:
            return getmonsterinfo(MustKill, MOB_JOBEXP) * KillAmount * $@JOB_RATE;
        case 3:
            return 5; // 5 Cash Point
        default:
            return 0;
    }
}

function	script	Searching	{
    set .@base_level, (($@MAX_LEVEL <= BaseLevel) ? $@MAX_LEVEL : BaseLevel);
    while (set(.@v, .@v+1)) {
        if (.@v % 10 == 0) {
            sleep2 1;
        }

        // 寫死隨機 蠍子 到 鹿角甲蟲
        set .@i, rand(1001, 2085);

        if (0 < getmonsterinfo(.@i, MOB_MVPEXP)) {
            continue;
        }

        for (set .@q, 0;.@q < getarraysize($@exception$); set .@q, .@q + 1) {
            if (compare(getmonsterinfo(.@i, MOB_NAME), $@exception$[.@q])) {
                continue;
            }
        }

        if (getmonsterinfo(.@i, MOB_LV) < .@base_level + $@MAX_LEVEL_RANGE &&
            getmonsterinfo(.@i, MOB_LV) > .@base_level - $@MIN_LEVEL_RANGE &&
            getmonsterinfo(.@i, MOB_BASEEXP) > 0 &&
            getmonsterinfo(.@i, MOB_JOBEXP) > 0) {
            break;
        }
    }
    return .@i;
}

function	script	CheckAmount	{
    if(getarg(0)>1 && getarg(0)<15) return rand(5,30);
    else if(getarg(0)<30) return rand(5,40);
    else if(getarg(0)>=30 && getarg(0) <50) return rand(40,50);
    else if(getarg(0)>=50 && getarg(0) <70) return rand(20,30);
    else if(getarg(0)>=70 && getarg(0) <90) return rand(10,25);
    else return rand(1,5);
}

fayon, 82, 129,5	script	魔物獵人	844,{
    mes .npcname$;
    mes "我將會以你現在的等級";
    mes "來分配工作給你，完成後會有報酬";
    mes "^ff0000※Lv.90UP 不適合此任務^000000";
    next;
    if(0 == getgmlevel() && (gettimetick(2) - #DAILY_QUESTS_HUNTER_TIMER) < 86400) {
        mes .npcname$;
        mes "- 延遲時間剩餘: ^ff0000" + (86400 - (gettimetick(2) - #DAILY_QUESTS_HUNTER_TIMER)) + "^000000 秒 -";
        close;
    }
    
    if (MustKill) {
        if(select(KillPoint >= KillAmount ? "提交任務(^888888任務已完成^000000)" : "查看任務內容", "放棄^FF1493'" + getmonsterinfo(MustKill, MOB_NAME) + "'^000000任務") == 2) {
            set MustKill, 0;
            set KillAmount, 0;
            set KillPoint, 0;
            if ($CheckMode) {
                set #DAILY_QUESTS_HUNTER_TIMER, gettimetick(2);
            }
            mes .npcname$;
            mes "^FF1493本次任務被取消^000000";
        } else if(KillPoint>=KillAmount){
            mes .npcname$;
            mes "恭喜你完成本次的魔物獵殺任務";
            mes "本次獲得的報酬";
            mes "Base經驗值 ^FF1493"+callfunc("Reward",1)+"^000000";
            mes "Job經驗值 ^FF1493"+callfunc("Reward",2)+"^000000";
            mes "Cash點數 ^FF1493"+callfunc("Reward",3)+"^000000";
            set BaseExp, BaseExp+callfunc("Reward",1);
            set JobExp, JobExp+callfunc("Reward",2);
            set #CASHPOINTS,#CASHPOINTS+callfunc("Reward",3);
            set MustKill,0;
            set KillAmount,0;
            set KillPoint,0;
            set #DAILY_QUESTS_HUNTER_TIMER, gettimetick(2);
        } else {
            mes .npcname$;
            mes "本次的魔物獵殺任務內容";
            mes "^0088FF魔物名稱^000000: ^FF1493"+getmonsterinfo(MustKill, MOB_NAME)+"^000000";
            mes "^0088FF魔物等級^000000: ^FF1493"+getmonsterinfo(MustKill, MOB_LV)+"";
            mes "^0088FF數量^000000:[ ^009900"+KillPoint+"^000000/^FF6600"+KillAmount+"^000000 ]";
            next;
            mes "本次的魔物獵殺任務酬勞";
            mes "^0088FFBase經驗值^000000: ^FF1493"+callfunc("Reward",1)+"^000000";
            mes "^0088FFJob經驗值^000000: ^FF1493"+callfunc("Reward",2)+"^000000";
            mes "^0088FFCash點數^000000: ^FF1493"+callfunc("Reward",3)+"^000000";
        }
        close;
    } else {
        set MustKill, callfunc("Searching");
        set KillAmount,callfunc("CheckAmount",getmonsterinfo(MustKill, MOB_LV));
        mes .npcname$;
        mes "本次的魔物獵殺任務內容";
        mes "^0088FF魔物名稱^000000: ^FF1493"+getmonsterinfo(MustKill, MOB_NAME)+"^000000";
        mes "^0088FF魔物等級^000000: ^FF1493"+getmonsterinfo(MustKill, MOB_LV)+"";
        mes "^0088FF數量^000000:[ ^009900"+KillPoint+"^000000/^FF6600"+KillAmount+"^000000 ]";
        next;
        mes "本次的魔物獵殺任務酬勞";
        mes "^0088FFBase經驗值^000000: ^FF1493"+callfunc("Reward",1)+"^000000";
        mes "^0088FFJob經驗值^000000: ^FF1493"+callfunc("Reward",2)+"^000000";
        mes "^0088FFCash點數^000000: ^FF1493"+callfunc("Reward",3)+"^000000";
        close;
    }

    OnNPCKillEvent:
        if (MustKill && KillPoint < KillAmount && killedrid == MustKill) {
            set KillPoint, KillPoint + 1;
            dispbottom "" + getmonsterinfo(MustKill, MOB_NAME) + " : [ " + KillPoint + "/" + KillAmount + " ]";
        }
        end;
    OnInit:
	waitingroom "打怪任務",0;
        set .npcname$, "[" + strnpcinfo(0) + "]";
        set $@MAX_LEVEL, 110; // 計算怪物最高等級基準
        set $@MAX_LEVEL_RANGE, 5; // 怪物最高等級範圍
        set $@MIN_LEVEL_RANGE, 10; // 怪物最小等級範圍

        set $@BASE_RATE, 5; // Base 經驗倍率
        set $@JOB_RATE, 5; // Job 經驗倍率
        logmes $@MAX_LEVEL_RANGE + "1234";
        // 避免搜尋到的魔物 (例: 華麗金屬)
        setarray $@exception$, "真晶", "炸彈", "聖誕", "肉粽", "寶箱", "箱子", "寶物箱子", "旗幟", "守護石", "防禦線", "水晶", "華麗金屬", "開關";
        end;
}